public threaded namespace thread::queue;
public import numeric::integral -;
public import container::array -;
public import pointer -;
public import operator -;
public import thread -;
public import callable -;
public import algebraic -;
public import meta m;

public metafunction task tcallable_ptr{void, m::nil};

public threaded struct task_queue(size_t num_thrs)
{
  public function void push(task const& e) {
    queue_push_notify(shared, e);
  }
  private function ~ {
    queue_thread_stop(shared, 1);
  }
  private function void construct(size_t num_thrs) {
    for (size_t i : 0 .. num_thrs) {
      thrs.push_back(make_thread_ptr{queue_thread_main}(shared));
    }
  }
  private tptr{task_queue_shared} const shared 
    = make_tptr{task_queue_shared}();
  private varray{callable_ptr{void, m::nil}} thrs;
  construct(num_thrs);
}

private multithreaded struct task_queue_shared()
{
  public deque{task} queue;
  public int stop_thread = 0;
}

private threaded function option{task}
queue_wait_pop(tptr{task_queue_shared} const& tq)
{
  option{task} ent;
  mutable lck = lock_guard{task_queue_shared}(tq);
  task_queue_shared mutable& q = *lck;
  while (true) {
    int const sf = q.stop_thread;
    bool const em = q.queue.empty();
    if (sf < 0 || (sf > 0 && em)) {
      break;
    }
    if (q.queue.empty()) {
      lck.wait();
      continue;
    }
    ent.some = q.queue.pop_front();
    break;
  }
  return ent;
}

private threaded function void
queue_thread_main(tptr{task_queue_shared} const& tq)
{
  while (true) {
    option{task} ent = queue_wait_pop(tq);
    if (case ent.none) {
      break;
    }
    ent.some.__call();
  }
}

private threaded function void
queue_thread_stop(tptr{task_queue_shared} const& tq, int mode)
{
  mutable lck = lock_guard{task_queue_shared}(tq);
  lck->stop_thread = mode;
  lck.notify_all();
}

private threaded function void
queue_push_notify(tptr{task_queue_shared} const& tq,
  task const& ep)
{
  mutable lck = lock_guard{task_queue_shared}(tq);
  task_queue_shared mutable& q = *lck;
  bool const need_notify = q.queue.empty();
  q.queue.push_back(ep);
  if (need_notify) {
    lck.notify_one();
  }
}

