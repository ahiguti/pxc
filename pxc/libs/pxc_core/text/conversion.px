public threaded namespace text::conversion;
public import numeric::integral -;
public import container::array -;
public import meta m;
public import meta::family mf;
public import numeric::cast n;
public import generic -;
public import text::positional p;
public import text::serialize -;
public import text::minimal_encode e;

public threaded function {t} string
to_string(t const& x)
{
  if (mf::is_string_or_slice_type{t}) {
    return x;
  } else {
    string s;
    serialize_to_string(x, s);
    return s;
  }
}

public threaded function {t} void
serialize_to_string(t const& x, string mutable& out)
{
  serialize{serialize_tpl, t}(x, out);
}

public threaded function {t} t
from_string(cstrref const& s)
{
  if (mf::is_string_or_slice_type{t}) {
    return s;
  } else {
    mutable s1 = s;
    return deserialize_from_string{t}(s1);
  }
}

public threaded function {t} void
from_string_emplace(cstrref const& s, t mutable& r)
{
  if (mf::is_string_or_slice_type{t}) {
    r = s;
  } else {
    deserialize_from_string_emplace(s, r);
  }
}

public threaded function {t} t
deserialize_from_string(cstrref mutable& s)
{
  return deserialize{deserialize_tpl, t}(s);
}

public threaded function {t} void
deserialize_from_string_emplace(cstrref mutable& s, t mutable& r)
{
  deserialize_emplace{deserialize_tpl, t}(s, r);
}

public threaded struct serialize_tpl {
  metafunction target_type string;
  metafunction specialized_func{t}
    m::cond{
      m::symbol_exists{m::nsof{t}, m::concat{t, "_append_to_string"}},
	m::symbol{m::nsof{t}, m::concat{t, "_append_to_string"}},
      mf::is_string_or_slice_type{t}, string_append_escape{t},
      mf::is_integral_type{t}, p::integral_to_decimal,
      mf::is_fp_type{t}, p::fp_to_decimal,
      m::eq{t, bool}, bool_to_string,
      m::eq{t, unit}, unit_to_string,
      0};
  metafunction array_begin string_append_char{'{'};
  metafunction array_end string_append_char{'}'};
  metafunction array_sep string_append_char{','};
  metafunction map_begin string_append_char{'{'};
  metafunction map_end string_append_char{'}'};
  metafunction map_sep string_append_char{','};
  metafunction map_entry_sep string_append_char{','};
  metafunction pointer_begin string_append_char{'{'};
  metafunction pointer_end string_append_char{'}'};
  metafunction struct_begin string_append_char{'{'};
  metafunction struct_end string_append_char{'}'};
  metafunction struct_sep string_append_char{','};
  metafunction struct_entry_sep string_append_char{','};
  metafunction struct_field_name string_append_noesc;
  metafunction union_begin string_append_char{'{'};
  metafunction union_end string_append_char{'}'};
  metafunction union_entry_sep string_append_char{','};
  metafunction union_field_name string_append_noesc;
  metafunction unknown{fam} unknown_to_string;
}

public threaded struct deserialize_tpl {
  metafunction source_type cstrref;
  metafunction specialized_func{t}
    m::cond{
      m::symbol_exists{m::nsof{t}, m::concat{t, "_parse_from_string"}},
	m::symbol{m::nsof{t}, m::concat{t, "_parse_from_string"}},
      mf::is_string_or_slice_type{t}, string_parse_string_unescape,
      mf::is_integral_type{t}, string_parse_integral{t},
      mf::is_fp_type{t}, string_parse_fp{t},
      m::eq{t, bool}, string_parse_bool,
      m::eq{t, unit}, string_parse_unit,
      0};
  metafunction array_begin_cond string_parse_char_cond{'{'};
  metafunction array_end_cond string_parse_char_cond{'}'};
  metafunction array_end_skip string_parse_char_skip{'{', '}'};
  metafunction array_sep_cond string_parse_char_cond{','};
  metafunction map_begin_cond string_parse_char_cond{'{'};
  metafunction map_end_cond string_parse_char_cond{'}'};
  metafunction map_end_skip string_parse_char_skip{'{', '}'};
  metafunction map_sep_cond string_parse_char_cond{','};
  metafunction map_entry_sep_cond string_parse_char_cond{','};
  metafunction pointer_begin_cond string_parse_char_cond{'{'};
  metafunction pointer_end_skip string_parse_char_skip{'{', '}'};
  metafunction struct_begin_cond string_parse_char_cond{'{'};
  metafunction struct_end_peek_cond string_parse_char_peek_cond{'}'};
  metafunction struct_end_skip string_parse_char_skip{'{', '}'};
  metafunction struct_sep_cond string_parse_char_cond{','};
  metafunction struct_entry_sep_cond string_parse_char_cond{','};
  metafunction struct_field_name_peek string_parse_symbol_peek;
  metafunction union_begin_cond string_parse_char_cond{'{'};
  metafunction union_end_cond string_parse_char_cond{'}'};
  metafunction union_end_skip string_parse_char_skip{'{', '}'};
  metafunction union_sep_cond string_parse_char_cond{','};
  metafunction union_entry_sep_cond string_parse_char_cond{','};
  metafunction union_field_name string_parse_symbol;
  metafunction unknown{fam} string_parse_token_skip;
  metafunction value_skip string_parse_token_skip;
}

private threaded function {t} void unknown_to_string(t const& v,
  string mutable& buf)
{
  string_append_char{'?'}(buf);
}

private threaded function void unit_to_string(unit v, string mutable& buf)
{
  string_append_char{'*'}(buf);
}

private threaded function void bool_to_string(bool v, string mutable& buf)
{
  if (v) {
    buf.append("1");
  } else {
    buf.append("0");
  }
}

private threaded function {t} void string_append_escape(t const& x,
  string mutable& v)
{
  e::minimal_encode(v, x);
}

private threaded function {t} void string_append_noesc(t const& x,
  string mutable& v)
{
  v.append(x);
}

private threaded function {ch} void string_append_char(string mutable& v)
{
  v.push_back(ch);
}

private threaded function void string_append_none(string mutable& v)
{
} 

private threaded function size_t token_length(cstrref const& s)
{
  for (size_t i, uchar ch : s) {
    if (ch == '{') {
      if (i == 0) {
	return find_brace_close(s[1 .. s.size()]) + 1;
      } else {
	return i;
      }
    }
    if (ch == '}' || ch == ',') {
      return i;
    }
  }
  return s.size();
}

private threaded function size_t find_brace_close(cstrref const& s)
{
  size_t level = 1;
  for (size_t i, uchar ch : s) {
    if (ch == '}') {
      if (--level == 0) {
	return i + 1;
      }
    } else if (ch == '{') {
      ++level;
    }
  }
  return s.size();
}

private threaded function {t, f} t string_parse_token(cstrref mutable& s)
{
  size_t const toklen = token_length(s);
  cstrref stok = s[0 .. toklen];
  s.increment_front(toklen);
  return f(stok);
}

private threaded function string string_parse_string_unescape(
  cstrref mutable& s)
{
  return string_parse_token{string, str_decode}(s);
  threaded function string str_decode(cstrref const& stok) {
    string r;
    e::minimal_decode(r, stok);
    return r;
  }
}

private threaded function {t} t string_parse_integral(cstrref mutable& s)
{
  return string_parse_token{t, p::decimal_to_integral{t}}(s);
}

private threaded function {t} t string_parse_fp(cstrref mutable& s)
{
  return string_parse_token{t, p::decimal_to_fp{t}}(s);
}

private threaded function bool string_parse_bool(cstrref mutable& s)
{
  return string_parse_token{bool, parse_bool}(s);
  threaded function bool parse_bool(cstrref const& stok) {
    if (stok.empty() || stok == "0") {
      return false;
    } else {
      return true;
    }
  }
}

private threaded function unit string_parse_unit(cstrref mutable& s)
{
  return unit();
}

private threaded function void string_parse_symbol_peek(cstrref mutable& stok)
{
  size_t const sz = stok.size();
  size_t const toklen = token_length(stok);
  stok.decrement_back(sz - toklen);
}

private threaded function void string_parse_symbol(cstrref mutable& stok,
  cstrref mutable& s)
{
  size_t const sz = stok.size();
  size_t const toklen = token_length(stok);
  stok.decrement_back(sz - toklen);
  s.increment_front(toklen);
}

private threaded function {ch} bool string_parse_char_cond(cstrref mutable& s)
{
  if (s.size() > 0 && s[0] == ch) {
    s.increment_front(1);
    return true;
  } else {
    return false;
  }
}

private threaded function {ch0, ch1} void string_parse_char_skip(
  cstrref mutable& s)
{
  size_t level = 1;
  for (size_t i, uchar ch : s) {
    if (ch == ch1) {
      if (--level == 0) {
	s.increment_front(i + 1);
	return;
      }
    } else if (ch == ch0) {
      ++level;
    }
  }
}

private threaded function void string_parse_token_skip(cstrref mutable& s)
{
  size_t const toklen = token_length(s);
  s.increment_front(toklen);
}

private threaded function {ch} bool string_parse_char_peek_cond(
  cstrref mutable& s)
{ return s.size() > 0 && s[0] == ch; }

