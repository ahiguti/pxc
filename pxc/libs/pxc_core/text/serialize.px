threaded namespace text::serialize;
public import numeric::integral -;
public import container::array -;
public import meta m;
public import meta::family mf;
public import numeric::cast -;

public threaded function {tpl, t} void serialize(t const& x,
  m::symbol{tpl, "target_type"} mutable& o)
{
  if (m::not{m::eq{m::apply{m::symbol{tpl, "specialized_func"}, t}, 0}}) {
    m::apply{m::symbol{tpl, "specialized_func"}, t}(x, o);
  } else if (mf::is_array_or_slice_type{t}) {
    m::symbol{tpl, "array_begin"}(o);
    for (size_t i, m::symbol{t, "mapped_type"} v : x) {
      if (i != 0) {
	m::symbol{tpl, "array_sep"}(o);
      }
      serialize{tpl, m::symbol{t, "mapped_type"}}(v, o);
    }
    m::symbol{tpl, "array_end"}(o);
  } else if (mf::is_map_or_range_type{t}) {
    m::symbol{tpl, "map_begin"}(o);
    bool is_first = true;
    for (m::symbol{t, "key_type"} k, m::symbol{t, "mapped_type"} v : x) {
      if (is_first) {
	is_first = false;
      } else {
	m::symbol{tpl, "map_sep"}(o);
      }
      serialize{tpl, m::symbol{t, "key_type"}}(k, o);
      m::symbol{tpl, "map_entry_sep"}(o);
      serialize{tpl, m::symbol{t, "mapped_type"}}(v, o);
    }
    m::symbol{tpl, "map_end"}(o);
  } else if (mf::is_pointer_type{t}) {
    m::symbol{tpl, "pointer_begin"}(o);
    serialize{tpl, m::at{t, 0}}(*x, o);
    m::symbol{tpl, "pointer_end"}(o);
  } else if (mf::is_struct_type{t}) {
    m::symbol{tpl, "struct_begin"}(o);
    expand (fld, idx : field_names_sorted{t}) {
      {
	if (idx != 0) {
	  m::symbol{tpl, "struct_sep"}(o);
	}
	m::symbol{tpl, "struct_field_name"}(m::to_string{fld}, o);
	m::symbol{tpl, "struct_entry_sep"}(o);
	serialize{tpl, m::at{field_types_sorted{t}, idx}}(x.fld, o);
      }
    }
    m::symbol{tpl, "struct_end"}(o);
  } else if (mf::is_union_type{t}) {
    m::symbol{tpl, "union_begin"}(o);
    expand (fld, idx : m::field_names{t}) {
      if (case x.fld) {
	m::symbol{tpl, "union_field_name"}(m::to_string{fld}, o);
	m::symbol{tpl, "union_entry_sep"}(o);
	serialize{tpl, m::at{m::field_types{t}, idx}}(x.fld, o);
      }
    }
    m::symbol{tpl, "union_end"}(o);
  } else {
    m::apply{m::symbol{tpl, "unknown"}, t}(x, o);
  }
}

public threaded function {tpl, t} t
deserialize(m::symbol{tpl, "source_type"} mutable& s)
{
  if (m::not{m::eq{m::apply{m::symbol{tpl, "specialized_func"}, t}, 0}}) {
    return m::apply{m::symbol{tpl, "specialized_func"}, t}(s);
  } else if (mf::is_array_or_slice_type{t}) {
    /* TODO: farray, darray */
    t r;
    if (m::symbol{tpl, "array_begin_cond"}(s)) {
      bool is_first = true;
      while (true) {
	if (s.empty() || m::symbol{tpl, "array_end_cond"}(s)) {
	  break;
	}
	if (is_first) {
	  is_first = false;
	} else {
	  m::symbol{tpl, "array_sep_cond"}(s);
	}
	m::at{t, 0} ent = deserialize{tpl, m::at{t, 0}}(s);
	r.push_back(ent);
      }
    } else {
      m::symbol{tpl, "value_skip"}(s);
    }
    return r;
  } else if (mf::is_map_or_range_type{t}) {
    t r;
    if (s.empty() || m::symbol{tpl, "map_begin_cond"}(s)) {
      bool is_first = true;
      while (true) {
	if (m::symbol{tpl, "map_end_cond"}(s)) {
	  break;
	}
	if (is_first) {
	  is_first = false;
	} else {
	  m::symbol{tpl, "map_sep_cond"}(s);
	}
	m::at{t, 0} k = deserialize{tpl, m::at{t, 0}}(s);
	if (!m::symbol{tpl, "map_entry_sep_cond"}(s)) {
	  m::symbol{tpl, "map_end_skip"}(s);
	  break;
	}
	m::at{t, 1} m = deserialize{tpl, m::at{t, 1}}(s);
	r[k] = m;
      }
    } else {
      m::symbol{tpl, "value_skip"}(s);
    }
    return r;
  } else if (mf::is_pointer_type{t}) {
    if (m::symbol{tpl, "pointer_begin_cond"}(s)) {
      t r = pointer::box_pointer{t}(deserialize{tpl, m::at{t, 0}}(s));
      m::symbol{tpl, "pointer_end_skip"}(s);
      return r;
    } else {
      cstrref s0;
      t r = pointer::box_pointer{t}(deserialize{tpl, m::at{t, 0}}(s0));
      return r;
    }
  } else if (mf::is_struct_type{t}) {
    t r;
    if (m::symbol{tpl, "struct_begin_cond"}(s)) {
      expand (fld, idx : field_names_sorted{t}) {
	{
	  if (!m::symbol{tpl, "struct_end_peek_cond"}(s)) {
	    bool found = true;
	    while (true) {
	      m::symbol{tpl, "struct_sep_cond"}(s);
	      m::symbol{tpl, "source_type"} s0 = s;
	      m::symbol{tpl, "struct_field_name_peek"}(s0);
	      if (s0 == m::to_string{fld}) {
		break;
	      } else if (s0 > m::to_string{fld}) {
		found = false;
		break;
	      }
	      s.increment_front(s0.size());
	      m::symbol{tpl, "struct_entry_sep_cond"}(s);
	      m::symbol{tpl, "value_skip"}(s);
	      if (s.empty() || m::symbol{tpl, "struct_end_peek_cond"}(s)) {
		found = false;
		break;
	      }
	    }
	    if (found) {
	      s.increment_front(m::strlen{m::to_string{fld}});
	      m::symbol{tpl, "struct_entry_sep_cond"}(s);
	      r.fld = deserialize{tpl, m::at{field_types_sorted{t}, idx}}(s);
	    }
	  }
	}
      }
      m::symbol{tpl, "struct_end_skip"}(s);
    } else {
      m::symbol{tpl, "value_skip"}(s);
    }
    return r;
  } else if (mf::is_union_type{t}) {
    t r;
    if (m::symbol{tpl, "union_begin_cond"}(s)) {
      bool is_first = true;
      while (true) {
	if (s.empty() || m::symbol{tpl, "union_end_cond"}(s)) {
	  break;
	}
	if (is_first) {
	  is_first = false;
	} else {
	  if (!m::symbol{tpl, "union_sep_cond"}(s)) {
	    m::symbol{tpl, "union_end_skip"}(s);
	    break;
	  }
	}
	m::symbol{tpl, "source_type"} s0 = s;
	m::symbol{tpl, "union_field_name"}(s0, s);
	if (!m::symbol{tpl, "union_entry_sep_cond"}(s)) {
	  m::symbol{tpl, "union_end_skip"}(s);
	  break;
	}
	expand (fld, idx : m::field_names{t}) {
	  if (s0 == m::to_string{fld}) {
	    r.fld = deserialize{tpl, m::at{m::field_types{t}, idx}}(s);
	  }
	}
      }
    } else {
      m::symbol{tpl, "value_skip"}(s);
    }
    return r;
  } else {
    t r;
    m::apply{m::symbol{tpl, "unknown"}, t}(s);
    return r;
  }
}

/* private */

private metafunction field_names_sorted{t}
  m::map{m::sort{m::fields{t}}, m::at0};
private metafunction field_types_sorted{t}
  m::map{m::sort{m::fields{t}}, m::at1};

