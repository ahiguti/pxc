threaded namespace io::standard "use-unsafe";
public import numeric::integral -;
public import container::array -;
public import io +;
public import io::file -;
public import io::errno -;
public import text::conversion -;
public import text::split -;
public import algebraic -;
public import meta::vararg v;
public import meta m;
public import meta::family -;

public threaded function extern "pxcio::io_stdin"
  file io_stdin(io const& iop);
public threaded function extern "pxcio::io_stdout"
  file io_stdout(io const& iop);
public threaded function extern "pxcio::io_stderr"
  file io_stderr(io const& iop);

public threaded function {t} void file_print(file const& f, t const& x)
{
  if (is_string_or_slice_type{t}) {
    f.write_all(x);
  } else {
    f.write_all(to_string(x));
  }
}

public threaded function {t} void file_println(file const& f, t const& x)
{
  if (is_string_or_slice_type{t}) {
    string s = x;
    s.append("\n");
    f.write_all(s);
  } else {
    string s = to_string(x);
    s.append("\n");
    f.write_all(s);
  }
}

public function {tlist} void print(expand(v::arg_decls_bycref{tlist}))
{
  string s = string_join{"\t"}(expand(a: v::arg_names{m::size{tlist}}; a));
  system.stdout().write_all(s);
}

public function {tlist} void println(expand(v::arg_decls_bycref{tlist}))
{
  string s = string_join{"\t"}(expand(a: v::arg_names{m::size{tlist}}; a));
  s.append("\n");
  system.stdout().write_all(s);
}

/* debug_log does not require an io handle */
public threaded function {tlist} void
debug_log(expand(v::arg_decls_bycref{tlist}))
{
  string s = string_join{"\t"}(expand(a: v::arg_names{m::size{tlist}}; a));
  s.push_back('\n');
  debug_log_internal(s);
}

private extern "pxcio::dummy" option{file} dummy;
  /* instantiate option{file} */

private threaded function extern "pxcio::debug_log_internal"
  void debug_log_internal(cstrref const& s);

extern "implementation" inline
namespace pxcio {

typedef ::algebraic$n::option$v$p$io$$file$n$$file$s$r$ optfile;

/* TODO: slow */
file io_stdin(io const& iop)  {
  optfile pxc_stdin;
  pxc_stdin.some$$l(make_file(0));
  return pxc_stdin.some$$r();
}
file io_stdout(io const& iop) {
  optfile pxc_stdout;
  pxc_stdout.some$$l(make_file(1));
  return pxc_stdout.some$$r();
}
file io_stderr(io const& iop) {
  optfile pxc_stderr;
  pxc_stderr.some$$l(make_file(2));
  return pxc_stderr.some$$r();
}
void debug_log_internal(cstrref const& s)
{
  write(2, s.rawarr(), s.size());
}

};
;

