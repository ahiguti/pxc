public threaded namespace pgl3d::texture::texture "use-unsafe";
public import common -;
public import pointer::raw -;
private import pointer::raw_unsafe -;
public import numeric::fpmath -;
public import text::serialize::compact_pod -;
public import glm;
private import glm::raw;
public import io::time -;
public import exception -;
public import meta m;
public import pointer::tfuncptr -;
public import pgl3d::engine::util -;
public import pgl3d::sdl::sdlutil -;
private import sdl2::api_base -;
private import sdl2::api_types -;
private import sdl2::api_functions -;
private import sdl2::api_wrappers -;
private import sdl2::misc -;

public tsvaluetype struct {t, b} array3_pot() private
{
  public metafunction mapped_type m::symbol{t, "mapped_type"};
  public metafunction mblk_l2 b;
  public uint width_l2;
  public uint height_l2;
  public uint depth_l2;
  public t data;
}

public threaded function {t} void
array3_pot_set_size_l2(t mutable& o, uint w_l2, uint h_l2, uint d_l2)
{
  metafunction mapped_type m::symbol{t, "mapped_type"};
  metafunction mblk_l2 m::symbol{t, "mblk_l2"};
  o.data.resize(0u, mapped_type());
  o.width_l2 = w_l2;
  o.height_l2 = h_l2;
  o.depth_l2 = d_l2;
  w_l2 = max(w_l2, mblk_l2);
  h_l2 = max(h_l2, mblk_l2);
  d_l2 = max(d_l2, mblk_l2);
  size_t sz = static_cast{size_t}(1u) << (w_l2 + h_l2 + d_l2);
  o.data.resize(sz, mapped_type());
}

public threaded function {t} glm::uvec3
array3_pot_get_size_uvec3(t const& o)
{
  return glm::make_uvec3(o.get_width(), o.get_height(), o.get_depth());
}

public threaded function {t} glm::uvec3
array3_pot_get_size_l2_uvec3(t const& o)
{
  return glm::make_uvec3(o.get_width_l2(), o.get_height_l2(),
    o.get_depth_l2());
}

public threaded function {t} uint
array3_pot_get_width_l2(t const& o)
{ return o.width_l2; }

public threaded function {t} uint
array3_pot_get_height_l2(t const& o)
{ return o.height_l2; }

public threaded function {t} uint
array3_pot_get_depth_l2(t const& o)
{ return o.depth_l2; }

public threaded function {t} uint
array3_pot_get_width(t const& o)
{ return 1u << o.width_l2; }

public threaded function {t} uint
array3_pot_get_height(t const& o)
{ return 1u << o.height_l2; }

public threaded function {t} uint
array3_pot_get_depth(t const& o)
{ return 1u << o.depth_l2; }

public threaded function {t} uint
array3_pot_index(t const& o, uint x, uint y, uint z)
{
  metafunction mblk_l2 m::symbol{t, "mblk_l2"};
  if (m::eq{mblk_l2, 0}) {
    return (z << (o.width_l2 + o.height_l2)) | (y << o.width_l2) | x;
  } else {
    uint const mblk_l2_u = mblk_l2;
    uint const mask = (1u << mblk_l2_u) - 1u;
    uint const xh = x >> mblk_l2_u;
    uint const yh = y >> mblk_l2_u;
    uint const zh = z >> mblk_l2_u;
    uint const xl = x & mask;
    uint const yl = y & mask;
    uint const zl = z & mask;
    uint const idxh =
      (zh << (o.width_l2 + o.height_l2 - mblk_l2_u * 2u)) |
      (yh << (o.width_l2 - mblk_l2_u)) |
      xh;
    uint const idxl =
      (zl << (mblk_l2_u * 2u)) |
      (yl << mblk_l2_u) |
      xl;
    const r = (idxh << (mblk_l2_u * 3u)) | idxl;
    /*
    // FIXME
    if (static_cast{size_t}(r) >= o.data.size()) {
      debug_log("invalid index r=", r, o.data.size());
      debug_log("xyz=", x, y, z);
      debug_log("obj=", o);
    }
    */
    return r;
  }
}

public threaded function {t} uint
array3_pot_size(t const& o)
{ return 1u << (o.width_l2 + o.height_l2 + o.depth_l2); }

public threaded function {t, tv} void
array3_pot_set(t mutable& o, uint x, uint y, uint z, tv v)
{
  o.data[o.index(x, y, z)] = v;
}
public threaded function {t} mapped_type
array3_pot_get(t const& o, uint x, uint y, uint z)
{
  metafunction mapped_type m::symbol{t, "mapped_type"};
  return o.data[o.index(x, y, z)];
}

public threaded function {f, t} void
array3_pot_foreach(t mutable& o)
{
  // TODO: reorder?
  for (const z: 0 .. o.get_depth()) {
    for (const y: 0 .. o.get_height()) {
      for (const x: 0 .. o.get_width()) {
	f(x, y, z, o.data[o.index(x, y, z)]);
      }
    }
  }
}

/*
public threaded function {t, tv} void
array3_pot_set_index(t mutable& o, uint i, tv v)
{
  o.data[i] = v;
}

public threaded function {t} mapped_type
array3_pot_get_index(t const& o, uint i)
{
  metafunction mapped_type m::symbol{t, "mapped_type"};
  return o.data[i];
}
*/

public threaded function {tpl, t} void
array3_pot_append_to_string(t const& o,
  m::find{tpl, "ser_target_type", 0} mutable& s)
{
  serialize_sequence{tpl}(o.width_l2, o.height_l2, o.depth_l2, o.data, s);
}

public threaded function {tpl, t} t
array3_pot_parse_from_string(m::find{tpl, "des_source_type", 0} mutable& s)
{
  t o;
  deserialize_sequence{tpl}(s, o.width_l2, o.height_l2, o.depth_l2, o.data);
  return o;
}

public threaded function {ta} m::at0{ta}
array3_pot_get_copy_linear(ta const& o)
{
  //debug_log("get_copy_linear", o.get_depth(), o.get_height(), o.get_width(), o.size());
  m::at0{ta} arr;
  arr.resize(o.size(), 0u);
  size_t i = 0;
  for (const z: 0 .. o.get_depth()) {
    for (const y: 0 .. o.get_height()) {
      for (const x: 0 .. o.get_width()) {
        arr[i] = o.get(x, y, z);
        ++i;
      }
    }
  }
  //debug_log("get_copy_linear done");
  return arr;
}

/*
public threaded function {ta} crawptr{mapped_type}
array3_pot_get_crawptr(ta const& o)
{
  metafunction t m::at0{ta};
  metafunction mapped_type m::symbol{t, "mapped_type"};
  if (m::or{m::symbol_exists{t, "to_crawptr"},
    m::symbol_exists{m::nsof{t}, m::concat{t, "_to_crawptr"}}}) {
    return o.data.to_crawptr();
  } else {
    return crawptr{mapped_type}();
  }
}
public threaded function {ta} rawptr{mapped_type}
array3_pot_get_rawptr(ta mutable& o)
{
  metafunction t m::at0{ta};
  metafunction mapped_type m::symbol{t, "mapped_type"};
  if (m::or{m::symbol_exists{t, "to_rawptr"},
    m::symbol_exists{m::nsof{t}, m::concat{t, "_to_rawptr"}}}) {
    return o.data.to_rawptr();
  } else {
    return rawptr{mapped_type}();
  }
}
*/

public threaded function {t} void
array3_pot_swap(t mutable& a, t mutable& b)
{
  swap(a.data, b.data);
  swap(a.width_l2, b.width_l2);
  swap(a.height_l2, b.height_l2);
  swap(a.depth_l2, b.depth_l2);
}

public metafunction texture_data array3_pot{varray{uint}, 0};

public threaded function {t0, t1} void
texture_data_copy_range(t0 mutable& dest,
  glm::uvec3 const& dpos, t1 const& src, glm::uvec3 const& spos,
  glm::uvec3 const& sz)
{
  for (uint z: 0 .. sz.z) {
    for (uint y: 0 .. sz.y) {
      for (uint x: 0 .. sz.x) {
	const v = src.get(spos.x + x, spos.y + y, spos.z + z);
	dest.set(dpos.x + x, dpos.y + y, dpos.z + z, v);
      }
    }
  }
}


public threaded function {t0} void
texture_data_fill_range(t0 mutable& dest,
  glm::uvec3 const& dpos, glm::uvec3 const& sz, uint v)
{
  for (uint z: 0 .. sz.z) {
    for (uint y: 0 .. sz.y) {
      for (uint x: 0 .. sz.x) {
	dest.set(dpos.x + x, dpos.y + y, dpos.z + z, v);
      }
    }
  }
}

public function ptr{texture_data}
load_texture_data_raw(string const& fn0)
{
  const p = make_ptr{texture_data}();
  string buf;
  sdl_load_file(fn0, buf);
  *p = from_string_compact_pod{texture_data}(buf);
  return p;
}

public function bool
save_texture_data_raw(string const& fn0, texture_data const& td)
{
  const buf = to_string_compact_pod(td);
  return sdl_save_file(fn0, buf);
}

public function varray{ptr{texture_data}}
load_texture_data_array_raw(string const& fn0)
{
  string buf;
  sdl_load_file(fn0, buf);
  return from_string_compact_pod{varray{ptr{texture_data}}}(buf);
}

public function bool
save_texture_data_array_raw(string const& fn0,
  varray{ptr{texture_data}} const& tds)
{
  const buf = to_string_compact_pod(tds);
  return sdl_save_file(fn0, buf);
}

metafunction SDL_Surface_Scoped scoped_nullable{SDL_FreeSurface};

public function ptr{texture_data}
load_texture_data(string const& fn0)
{
  debug_log("load_texture_data", fn0);
  const p = make_ptr{texture_data}();
  string fn = fn0;
  fn.push_back(0);
  SDL_Surface_Scoped sur0;
  sur0.reset(IMG_Load(fn.to_crawptr().to_ccharptr()));
  if (sur0.get() == rawptr{SDL_Surface}()) {
    sdl_log("failed to load", fn);
    throw runtime_error_template{"img_load"}(fn0);
  }
  sdl_log("loading image: loaded", fn);
  debug_log("load_texture_data p0");
  const& sur0ref = *sur0.get().to_slice(1);
  const fmtp = sur0ref.format;
  const& fmt = *sur0ref.format.to_slice(1);
  const bpp = fmt.BytesPerPixel;
  if (bpp != 4 && bpp != 3) {
    throw runtime_error_template{"img_load"}("bpp");
  }
  debug_log("load_texture_data p1");
  const sur0_w = static_cast{uint}(sur0ref.w);
  const sur0_h = static_cast{uint}(sur0ref.h);
  debug_log("load_texture_data p2", sur0_w, sur0_h);
  mutable tex_w = power_of_2(sur0_w);
  mutable tex_h = power_of_2(sur0_h);
  p->set_size_l2(integral_log2(tex_w), integral_log2(tex_h), 0);
  mutable& buf = *p;
  uint rmask = fmt.Rmask;
  uint gmask = fmt.Gmask;
  uint bmask = fmt.Bmask;
  uint amask = fmt.Amask;
  function uint get_shift(uint m) {
    uint r = 0;
    for (uint i: 0 .. 32) {
      if ((m & 1) != 0) {
	break;
      }
      r += 1;
      m >>= 1;
    }
    return r;
  }
  uint rshift = get_shift(rmask);
  uint gshift = get_shift(gmask);
  uint bshift = get_shift(bmask);
  uint ashift = get_shift(amask);
  const& sur0_ref = *sur0.get().to_slice(1);
  const sur0buf = sur0_ref.pixels.reinterpret_cast{uchar}()
    .to_slice(static_cast{size_t}(sur0_ref.pitch * sur0_ref.h));
  debug_log("load_texture_data p3", sur0_w, sur0_h);
  for (const y: 0 .. sur0_h) {
    for (const x: 0 .. sur0_w) {
      const o = (y * sur0_w + x) * bpp;
      // debug_log("load_texture_data p4r", x, y, o);
      uint v;
      uint vr;
      uint vg;
      uint vb;
      uint va;
      if (bpp == 3) {
	v = read_3_uint(sur0buf, o);
      } else if (bpp == 4) {
	v = read_4_uint(sur0buf, o);
      }
      // debug_log("load_texture_data p5r", v);
      // debug_log("load_texture_data p5r00", v);
      // debug_log("load_texture_data p5r0", rshift, gshift, bshift);
      // debug_log("load_texture_data p5r030", (v >> 16));
      // debug_log("load_texture_data p5r031", (v >> 8));
      // debug_log("load_texture_data p5r032", (v >> 0));
      // debug_log("load_texture_data p5r03", (v >> bshift));
      vb = (v >> bshift) & 0xffU;
      // debug_log("load_texture_data p5r02", vb);
      vg = (v >> gshift) & 0xffU;
      // debug_log("load_texture_data p5r02", vg);
      vr = (v >> rshift) & 0xffU;
      // debug_log("load_texture_data p5r02", vr);
      // debug_log("load_texture_data p5r1", vr, vg, vb);
      if (bpp == 4) {
	va = (v >> ashift) & 0xffU;
      }
      // debug_log("load_texture_data p5r2", va);
      uint v1 = (va << 24) | (vb << 16) | (vg << 8) | (vr << 0);
      // debug_log("load_texture_data p5r3", v1);
      // debug_log("load_texture_data p7r", x, y, tex_w, x * tex_w + x);
      buf.set(x, y, 0, v1);
      // debug_log("load_texture_data p6r", y * tex_w + x, v1);
    }
  }
  debug_log("load_texture_data done", fn0);
  return p;
}

