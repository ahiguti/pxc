private threaded namespace pgl3d::drawer::aberration "export-unsafe";
public import common -;
public import pgl3d::drawer::base -;
public import meta m;

private tsvaluetype struct aberration_vertex_attributes()
{
  public glm::vec2 vert;
}

private tsvaluetype struct aberration_instance_attributes()
{
}

private tsvaluetype struct aberration_uniforms()
{
  public int sampler_tex;
}

public struct drawer_aberration(shader_config const& glc,
  drawer_shared const& ds, string const& src_fbname0)
<drawstep_i>
{
  metafunction tattr aberration_vertex_attributes;
  string const src_fbname = src_fbname0;
  option{cptr{gl_framebuffer}} srcfbo;
  aberration_shader_ptr sdr_re = init_aberration_shader(glc);
  vbid_t const vbid_aberration = ds.vbid_pool->intern("vbid_generic_filter");
  init(ds);
  function void init(drawer_shared const& ds) {
    const respool = pointer_downcast{ptr{resource_pool}}(ds.gl_resources);
    if (!src_fbname.empty()) {
      srcfbo.some = *respool->framebuffers.equal_range(src_fbname);
    }
  }
  public function vbid_t get_vbid() const { return vbid_aberration; }
  public function vbid_t get_instances_vbid() const {
    return vbid_aberration; }
  public function vbid_t get_vertices_vbid() const { return vbid_aberration; }
  public function prepare_instance_data_funcptr
  get_prepare_instance_data_funcptr() const {
    return make_tfuncptr{prepare_instance_data_default}();
  }
  public function ptr{vertex_buffer} make_vertex_buffer() const {
    return make_tattr_vertex_buffer{tattr}(false, false);
  }
  public function string builtin_vertex_set_name() const {
    return "generic_filter"; }
  public function void make_builtin_vertex_sets(ptr{vertices} const& vptr)
    const
  {
    farray{vertex_attr, 4} dummy_vattr;
    farray{aberration_vertex_attributes, 4} vs;
    vs[0].vert = glm::make_vec2(-1.0, -1.0);
    vs[1].vert = glm::make_vec2( 1.0, -1.0);
    vs[2].vert = glm::make_vec2( 1.0,  1.0);
    vs[3].vert = glm::make_vec2(-1.0,  1.0);
    vptr->push_polygon_distinct{aberration_vertex_attributes, 0}(
      dummy_vattr, vs);
  }
  public function dgf_filler make_filler(ptr{vertices} const& vptr) const
  {
    return make_dgf_noop(vptr);
  }
  public function texture_info get_texture_info(cstrref const& name) const
  {
    return texture_info();
  }
  public function void resize_screen(shader_config const& glc)
  {
  }
  public function bool reload_shaders(shader_config const& glc)
  {
    sdr_re = init_aberration_shader(glc);
    return true;
  }
  public function bool has_step(draw_step step) const
  {
    if (step == draw_step_postprocess) {
      return true;
    }
    return false;
  }
  public function void draw_drawstep(draw_data mutable& ddata)
  {
    // debug_log("aberration draw");
    glDisable(GL_DEPTH_TEST);
    glDepthMask(0);
    glActiveTexture(GL_TEXTURE0);
    glBindTexture(GL_TEXTURE_2D, srcfbo.some->texture_color[0].get());
    mutable& sdr = *sdr_re;
    glUseProgram(sdr.program);
    glUniform1i(sdr.u_sampler_tex, 0);
    const& bo = *ddata.vertex_buffers[get_vertices_vbid()];
    bo.draw(0, sdr);
  }
  public function option{ptr{edit_mode_i}} get_edit_mode() const
  {
    return option{ptr{edit_mode_i}}();
  }
}

metafunction aberration_shader_ptr
  ptr{glshader{
    aberration_uniforms,
    aberration_instance_attributes,
    aberration_vertex_attributes}};

function aberration_shader_ptr
init_aberration_shader(shader_config const& g)
{
  tree_map{string, string} bnd;
  const v = load_shader_program(g, bnd, "aberration.vsh");
  const f = load_shader_program(g, bnd, "aberration.fsh");
  return make_glshader_ptr{
    aberration_uniforms,
    aberration_instance_attributes,
    aberration_vertex_attributes}(v, f, "", g.debug_level, "aberration");
}

