private threaded namespace pgl3d::drawer::fxaa "export-unsafe";
public import common -;
public import pgl3d::drawer::base -;
public import meta m;

private tsvaluetype struct fxaa_vertex_attributes()
{
  public glm::vec2 vert;
}

private tsvaluetype struct fxaa_instance_attributes()
{
}

private tsvaluetype struct fxaa_uniforms()
{
  public glm::vec2 pixel_delta;
  public int sampler_tex;
  public float option_value;
}

public tsvaluetype struct drawer_fxaa_params {
  public float option_value;
}

public struct drawer_fxaa(shader_config const& glc,
  drawer_shared const& ds, ptr{drawer_fxaa_params} const& params0,
  string const& src_fbname0)
<drawstep_i>
{
  metafunction tattr fxaa_vertex_attributes;
  ptr{drawer_fxaa_params} const params = params0;
  public string const src_fbname = src_fbname0;
  public ptr{gl_framebuffer} ppfbo = make_ptr{gl_framebuffer}();
  public option{cptr{gl_framebuffer}} srcfbo;
  fxaa_shader_ptr sdr_re = init_fxaa_shader(glc);
  vbid_t const vbid_fxaa = ds.vbid_pool->intern_create("vbid_generic_filter");
  init(glc, ds);
  function void init(shader_config const& glc, drawer_shared const& ds) {
    const respool = pointer_downcast{ptr{resource_pool}}(ds.gl_resources);
    if (const& fb: respool->framebuffers[src_fbname]) {
      srcfbo.some = fb;
    }
  }
  public function vbid_t get_vbid() const { return vbid_fxaa; }
  public function vbid_t get_instances_vbid() const { return vbid_fxaa; }
  public function vbid_t get_vertices_vbid() const { return vbid_fxaa; }
  public function prepare_instance_data_funcptr
  get_prepare_instance_data_funcptr() const {
    return make_tfuncptr{prepare_instance_data_default}();
  }
  public function ptr{vertex_buffer} make_vertex_buffer() const {
    return make_tattr_vertex_buffer{tattr}(false, false);
  }
  public function string builtin_vertex_set_name() const {
    return "generic_filter"; }
  public function void make_builtin_vertex_sets(ptr{vertices} const& vptr)
    const
  {
    farray{vertex_attr, 4} dummy_vattrs;
    farray{fxaa_vertex_attributes, 4} vs;
    vs[0].vert = glm::make_vec2(-1.0, -1.0);
    vs[1].vert = glm::make_vec2( 1.0, -1.0);
    vs[2].vert = glm::make_vec2( 1.0,  1.0);
    vs[3].vert = glm::make_vec2(-1.0,  1.0);
    vptr->push_polygon_distinct{fxaa_vertex_attributes, 0}(dummy_vattrs, vs);
  }
  public function dgf_filler make_filler(ptr{vertices} const& vptr) const
  {
    return make_dgf_noop(vptr);
  }
  public function texture_info get_texture_info(cstrref const& name) const
  {
    return texture_info();
  }
  public function void resize_screen(shader_config const& glc)
  {
  }
  public function bool reload_shaders(shader_config const& glc)
  {
    return true;
  }
  public function void draw_drawstep(draw_data mutable& ddata)
  {
    // debug_log("fxaa draw");
    const& fb = *srcfbo.some;
    glDisable(GL_DEPTH_TEST);
    glDepthMask(0);
    glActiveTexture(GL_TEXTURE0);
    glBindTexture(GL_TEXTURE_2D, fb.texture_color[0].get());
    mutable& sdr = *sdr_re;
    glUseProgram(sdr.program);
    glUniform1i(sdr.u_sampler_tex, 0);
    const pixel_delta = glm::make_vec2(
      1.0f / static_cast{float}(fb.width),
      1.0f / static_cast{float}(fb.height));
    // debug_log("delta", pixel_delta);
    glUniform2fv(sdr.u_pixel_delta, 1, pixel_delta.to_crawptr());
    mutable opval = static_cast{int}(params->option_value);
    opval %= 2;
    glUniform1f(sdr.u_option_value, static_cast{float}(opval));
    const& bo = *ddata.vertex_buffers[get_vertices_vbid()];
    bo.draw(0, sdr);
  }
  public function option{ptr{edit_mode_i}} get_edit_mode() const
  {
    return option{ptr{edit_mode_i}}();
  }
}

metafunction fxaa_shader_ptr
  ptr{glshader{
    fxaa_uniforms, fxaa_instance_attributes, fxaa_vertex_attributes}};

function fxaa_shader_ptr
init_fxaa_shader(shader_config const& g)
{
  tree_map{string, string} bnd;
  const v = load_shader_program(g, bnd, "fxaa.vsh");
  const f = load_shader_program(g, bnd, "fxaa.fsh");
  return make_glshader_ptr{fxaa_uniforms, fxaa_instance_attributes,
    fxaa_vertex_attributes}(v, f, "", g.debug_level, "fxaa");
}

