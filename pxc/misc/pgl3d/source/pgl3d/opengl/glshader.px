private threaded namespace pgl3d::opengl::glshader "export-unsafe";
public import common -;
public import pointer::raw -;
public import pointer::raw_unsafe -;
public import numeric::fpmath -;
public import glm;
public import opengl::compat -;
public import meta m;
public import exception -;
public import pgl3d::opengl::glresource -;
public import pgl3d::sdl::sdlutil -;

public metafunction num_float_struct{t}
  m::fold{m::map{m::field_types{t}, num_float_fld}, m::add, 0};

public metafunction num_float_fld{t}
  m::cond{
    m::eq{t, float}, 1,
    m::eq{t, int}, 1,
    m::eq{t, unit}, 0,
    m::eq{t, glm::vec2}, 2,
    m::eq{t, glm::vec3}, 3,
    m::eq{t, glm::vec4}, 4,
    m::eq{t, glm::mat2}, 4,
    m::eq{t, glm::mat3}, 9,
    m::eq{t, glm::mat4}, 16,
    m::error{"invalid type"}};

public metafunction num_attrib_fld{t}
  m::cond{
    m::eq{t, float}, 1,
    m::eq{t, int}, 1,
    m::eq{t, unit}, 0,
    m::eq{t, glm::vec2}, 1,
    m::eq{t, glm::vec3}, 1,
    m::eq{t, glm::vec4}, 1,
    m::eq{t, glm::mat2}, 2,
    m::eq{t, glm::mat3}, 3,
    m::eq{t, glm::mat4}, 4,
    m::error{"invalid type"}};

public interface glshader_i {
  public function void enable_vertex_attrib_array(size_t num_float_per_vertex);
  public function void disable_vertex_attrib_array();
  public function void enable_instance_attrib_array(bool enable_flag);
  public function void instance_attrib_pointer(size_t offset_float);
  public function size_t instance_attrib_num_float() const;
  public function void instance_attrib_set_uniform(cslice{float} const& value,
    size_t offset, size_t cnt);
}


public function {tuni, tinst, tvert} ptr{glshader{tuni, tinst, tvert}}
make_glshader_ptr(cstrref const& prog_v, cstrref const& prog_f,
  strlit const& attrib_to_avoid_0, int debug_level, cstrref const& fname_hint)
{
  return make_ptr{glshader{tuni, tinst, tvert}}(
    prog_v, prog_f, attrib_to_avoid_0, debug_level, fname_hint);
}

private function {t} void
set_uniform(GLint loc, cslice{float} const& value, size_t offset, int cnt)
{
  if (m::eq{t, glm::mat4}) {
    glUniformMatrix4fv(loc, cnt, 0, value.to_crawptr() + offset);
  } else if (m::eq{t, glm::mat3}) {
    glUniformMatrix3fv(loc, cnt, 0, value.to_crawptr() + offset);
  } else if (m::eq{t, glm::mat2}) {
    glUniformMatrix2fv(loc, cnt, 0, value.to_crawptr() + offset);
  } else if (m::eq{t, glm::vec4}) {
    glUniform4fv(loc, cnt, value.to_crawptr() + offset);
  } else if (m::eq{t, glm::vec3}) {
    glUniform3fv(loc, cnt, value.to_crawptr() + offset);
  } else if (m::eq{t, glm::vec2}) {
    glUniform2fv(loc, cnt, value.to_crawptr() + offset);
  } else if (m::eq{t, float}) {
    glUniform1fv(loc, cnt, value.to_crawptr() + offset);
  } else {
    m::error{"set_uniform"};
  }
}

public struct {tuni, tinst, tvert}
glshader(cstrref const& prog_v, cstrref const& prog_f,
  strlit const& attrib_to_avoid_0, int debug_level, cstrref const& fname_hint)
<glshader_i>
{
  public metafunction uniforms_type tuni;
  public metafunction instance_attributes_type tinst;
  public metafunction vertex_attributes_type tvert;
  expand (sym: m::map{m::field_names{tuni},
    metafunction{s} m::concat{"u_", s}})
  {
    public int sym = -1;
  }
  expand (sym: m::map{m::field_names{tinst},
    metafunction{s} m::concat{"i_", s}})
  {
    public int sym = -1;
  }
  expand (sym: m::map{m::field_names{tvert},
    metafunction{s} m::concat{"v_", s}})
  {
    public int sym = -1;
  }
  public uint program = 0;
  public string error;
  /* private */
  private function ~
  {
    glDeleteProgram(program); /* ok even if program == 0 */
  }
  init(prog_v, prog_f, attrib_to_avoid_0, debug_level, fname_hint);
  private function void init(cstrref const& prog_v, cstrref const& prog_f,
    strlit const& attrib_to_avoid_0, int debug_level,
    cstrref const& fname_hint)
  {
    program = create_program(prog_v, prog_f, attrib_to_avoid_0, debug_level,
      error);
    if (program == 0) {
      sdl_log("create_program: ", error);
      throw runtime_error_template{"create_program"}("1");
    }
    expand (sym: m::field_names{tuni}) {
      expand (vsym: {m::concat{"u_", sym}}) {
	vsym = glGetUniformLocation_nocheck(program, m::to_string{sym});
	glGetError(); /* ignore */
	if (vsym < 0) {
	  /* model_matrix is defined as a vertex attribute if instanced
	   * rendering is enabled. */
	  vsym = glGetAttribLocation_nocheck(program, m::to_string{sym});
	  glGetError(); /* ignore */
	  sdl_log("uniform(attr)", m::to_string{sym}, vsym);
	  /*
	  */
	} else {
	  sdl_log("uniform", m::to_string{sym}, vsym);
	  /*
	  */
	}
      }
    }
    expand (sym: m::field_names{tinst}) {
      expand (vsym: {m::concat{"i_", sym}}) {
	vsym = glGetUniformLocation_nocheck(program, m::to_string{sym});
	glGetError(); /* ignore */
	if (vsym < 0) {
	  /* model_matrix is defined as a vertex attribute if instanced
	   * rendering is enabled. */
	  vsym = glGetAttribLocation_nocheck(program, m::to_string{sym});
	  glGetError(); /* ignore */
	  sdl_log("instance(attr)", m::to_string{sym}, vsym);
	  /*
	  */
	} else {
	  sdl_log("instance", m::to_string{sym}, vsym);
	  /*
	  */
	}
      }
    }
    expand (sym: m::field_names{tvert}) {
      expand (vsym: {m::concat{"v_", sym}}) {
	vsym = glGetAttribLocation_nocheck(program, m::to_string{sym});
	glGetError(); /* ignore */
	/*
	sdl_log("attribute", m::to_string{sym}, vsym);
	*/
      }
    }
    if (debug_level > 0) {
      dump_program_binary(fname_hint);
    }
  }
  public function void instance_attrib_prepare(slice{float} const& arr,
    instance_attributes_type const& v)
  {
    size_t offset = 0;
    metafunction tinst_sh instance_attributes_type;
    expand (sym, idx: m::field_names{tinst_sh})
    {
      {
	metafunction ft m::at{m::field_types{tinst_sh}, idx};
	size_t const nfl = num_float_fld{ft};
	/* TODO: generic */
	if (m::eq{ft, float}) {
	  arr[offset] = v.sym;
	} else {
	  const src = v.sym.to_cslice();
	  for (const i: 0 .. nfl) {
	    arr[offset + i] = src[i];
	  }
	}
	offset += nfl;
      }
    }
  }
  public function void enable_instance_attrib_array(bool enable_flag)
  {
    size_t offset = 0;
    metafunction tinst_sh instance_attributes_type;
    expand (sym, idx: m::map{m::field_names{tinst_sh},
      metafunction{s} m::concat{"i_", s}})
    {
      {
	metafunction tfield m::at{m::field_types{tinst_sh}, idx};
	size_t const nfl = num_float_fld{tfield};
	if (sym >= 0) {
	  if (m::ne{is_gl33_or_gles3, 0}) {
	    int const n = num_attrib_fld{tfield};
	    for (const i: 0 .. n) {
	      const loc = static_cast{uint}(sym + i);
	      if (enable_flag) {
		// sdl_log("enable", loc);
		glEnableVertexAttribArray(loc);
		glVertexAttribDivisor(loc, 1);
	      } else {
		// sdl_log("disable", loc);
		glVertexAttribDivisor(loc, 0);
		glDisableVertexAttribArray(loc);
	      }
	    }
	  } else {
	    throw runtime_error_template{"enable_instance_attrib_array"}("");
	  }
	}
	offset += nfl;
      }
    }
  }
  public function void instance_attrib_pointer(size_t offset_float)
  {
    size_t offset = 0;
    metafunction tinst_sh instance_attributes_type;
    metafunction stride_byte m::mul{num_float_struct{tinst_sh}, 4};
    expand (sym, idx: m::map{m::field_names{tinst_sh},
      metafunction{s} m::concat{"i_", s}})
    {
      {
	metafunction tfield m::at{m::field_types{tinst_sh}, idx};
	size_t const nfl = num_float_fld{tfield};
	if (sym >= 0) {
	  if (m::ne{is_gl33_or_gles3, 0}) {
	    int const dv =
	      m::div{num_float_fld{tfield}, num_attrib_fld{tfield}};
	    int const n = num_attrib_fld{tfield};
	    for (const i: 0 .. n) {
	      const loc = static_cast{uint}(sym + i);
	      size_t const o =
		(offset_float + offset + static_cast{size_t}(i * dv)) * 4U;
	      // sdl_log("ptr", loc, stride_byte, offset, i, dv, o);
	      glVertexAttribPointer(loc, dv, GL_FLOAT, 0, stride_byte,
		offset_to_void_cp(o));
	    }
	  } else {
	    throw runtime_error_template{"instance_attrib_pointer"}("");
	  }
	}
	offset += nfl;
      }
    }
  }
  public function size_t instance_attrib_num_float() const
  {
    return num_float_struct{instance_attributes_type};
  }
  public function void instance_attrib_set_uniform(cslice{float} const& arr,
    size_t offset, size_t cnt)
  {
    metafunction tinst_sh instance_attributes_type;
    metafunction stride_byte m::mul{num_float_struct{tinst_sh}, 4};
    if (m::gt{m::size{m::field_names{tinst_sh}}, 1}) {
      m::error{"not supported"};
    }
    expand (sym, idx: m::map{m::field_names{tinst_sh},
      metafunction{s} m::concat{"i_", s}})
    {
      {
	metafunction tfield m::at{m::field_types{tinst_sh}, idx};
	size_t const nfl = num_float_fld{tfield};
	if (sym >= 0) {
	  set_uniform{tfield}(sym, arr, offset, static_cast{int}(cnt));
//sdl_log("set_uniform", m::to_string{sym}, arr[offset .. offset + nfl]);
	}
	offset += nfl;
      }
    }
  }
  public function void enable_vertex_attrib_array(size_t num_float_per_vertex)
  {
    int const stride_byte = static_cast{int}(num_float_per_vertex * 4);
    size_t offset = 0;
    metafunction tvert_sh vertex_attributes_type;
    expand (sym, idx: m::map{m::field_names{tvert_sh},
      metafunction{s} m::concat{"v_", s}})
    {
      if (sym >= 0) {
	int const nfloat = num_float_fld{m::at{m::field_types{tvert_sh}, idx}};
	glEnableVertexAttribArray(static_cast{uint}(sym));
	glVertexAttribPointer(static_cast{uint}(sym), nfloat, GL_FLOAT, 0,
	  stride_byte, offset_to_void_cp(offset));
//sdl_log("vap", m::to_string{sym}, sym, "offset", offset, "float", nfloat, "stride_byte", stride_byte);
	offset += static_cast{size_t}(nfloat) * 4;
      } else {
//sdl_log("vap notfound", m::to_string{sym}, sym);
	int const nfloat = num_float_fld{m::at{m::field_types{tvert_sh}, idx}};
	offset += static_cast{size_t}(nfloat) * 4;
      }
    }
  }
  public function void disable_vertex_attrib_array()
  {
    metafunction tvert_sh vertex_attributes_type;
    expand (sym, idx: m::map{m::field_names{tvert_sh},
      metafunction{s} m::concat{"v_", s}})
    {
      if (sym >= 0) {
	glDisableVertexAttribArray(static_cast{uint}(sym));
      }
    }
  }
  private function void dump_program_binary(cstrref const& fname_hint)
  {
    if (m::is_true{is_gl45}) {
      GLsizei len;
      // glGetProgramiv(prog, GL_PROGRAM_BINARY_LENGTH, address(len));
      string buf;
      // buf.resize(static_cast{size_t}(len), 0);
      buf.resize(1024u * 1024u * 1024u, 0);
      GLenum binfmt;
      glGetProgramBinary(program, static_cast{GLsizei}(buf.size()),
	address(len), address(binfmt), buf.to_rawptr().to_voidptr());
      buf.resize(static_cast{size_t}(len), 0);
      sdl_save_file("glprog." + fname_hint + ".bin", buf);
    }
  }
}

private function string format_glsl(cstrref const& src)
{
  string r;
  size_t ln = 1;
  split_foreach{function void(cstrref const& line) {
    r += to_string(ln);
    r += ":\t";
    r.append(line);
    r.append("\n");
    ++ln;
  }}(src, '\n');
  return r;
}

private function void glsl_format_log(cstrref const& src)
{
  const lines = format_glsl(src);
  split_foreach{function void(cstrref const& line) {
    debug_log(line);
  }}(lines, '\n');
}

private function uint compile_shader(cstrref const& src, GLenum type,
  int debug_level, string mutable& err_r)
{
  mutable res = glCreateShader(type);
  mutable source = make_farray{ccharptr}(src.to_crawptr().to_ccharptr());
  const length = make_farray{int}(static_cast{int}(src.size()));
  glShaderSource(res, 1, source.to_rawptr(), length.to_crawptr());
  glCompileShader(res);
  int success;
  glGetShaderiv(res, GL_COMPILE_STATUS, address(success));
  if (success == 0) {
    int sz;
//    if (m::is_true{is_android}) {
    //if (m::eq{m::profile{"platform"}, "android"}) {
//      sz = 4096; /* android bug workaround */
//    } else
    {
      glGetShaderiv(res, GL_INFO_LOG_LENGTH, address(sz));
      if (sz == 0) {
	sz = 4096; // android bug workaround
      }
    }
    string buf;
    buf.resize(static_cast{size_t}(sz), 0);
    GLsizei len;
    glGetShaderInfoLog(res, static_cast{GLsizei}(sz), address(len),
      buf.to_rawptr().to_charptr());
    if (buf.size() > 0 && buf[buf.size() - 1] == 0) {
      buf.resize(buf.size() - 1, 0); // drop trailing nul
    }
    sdl_log("compile_shader failed:", static_cast{int}(type), success, buf);
    debug_log("compile_shader failed:", static_cast{int}(type), success, buf);
    glsl_format_log(src);
    err_r = string_join{"\t"}(static_cast{int}(type), success, buf);
    glDeleteShader(res);
    res = 0;
  }
  return res;
}

private function uint
create_program(cstrref const& vertex_shader, cstrref const& fragment_shader,
  strlit const& attrib_to_avoid_0, int debug_level, string mutable& err_r)
{
  mutable prog = glCreateProgram();
  uint shader;
  int success = 1;
  if (!vertex_shader.empty()) {
    shader = compile_shader(vertex_shader, GL_VERTEX_SHADER, debug_level,
      err_r);
    if (shader != 0) {
      glAttachShader(prog, shader);
      glDeleteShader(shader); /* shader will be delete when prog is deleted */
    } else {
      success = 0;
    }
  }
  if (!fragment_shader.empty()) {
    shader = compile_shader(fragment_shader, GL_FRAGMENT_SHADER, debug_level,
      err_r);
    if (shader != 0) {
      glAttachShader(prog, shader);
      glDeleteShader(shader); /* shader will be delete when prog is deleted */
      if (!attrib_to_avoid_0.empty()) {
	glBindAttribLocation(prog, 1,
	  attrib_to_avoid_0.to_crawptr().to_ccharptr());
      }
    } else {
      success = 0;
    }
  }
  if (success == 0) {
    debug_log("create_program failed:", success);
    glDeleteProgram(prog);
    prog = 0;
  } else {
    glLinkProgram(prog);
    int linkst;
    glGetProgramiv(prog, GL_LINK_STATUS, address(linkst));
    if (linkst == 0) {
      string buf;
      buf.resize(4096, 0);
      GLsizei len;
      glGetProgramInfoLog(prog, static_cast{GLsizei}(buf.size()), address(len),
	buf.to_rawptr().to_charptr());
      buf.resize(static_cast{size_t}(len), 0);
      debug_log("linkprogram failed:", prog, buf);
      glsl_format_log(vertex_shader);
      glsl_format_log(fragment_shader);
      // debug_log("vert:", vertex_shader);
      // debug_log("frag:", fragment_shader);
      glDeleteProgram(prog);
      prog = 0;
    } else {
      debug_log("linkprogram succeeded:", prog);
      /*
      GLsizei len;
      // glGetProgramiv(prog, GL_PROGRAM_BINARY_LENGTH, address(len));
      string buf;
      // buf.resize(static_cast{size_t}(len), 0);
      buf.resize(1024u * 1024u * 32u, 0);
      GLenum binfmt;
      glGetProgramBinary(prog, static_cast{GLsizei}(buf.size()), address(len),
	address(binfmt), buf.to_rawptr().to_voidptr());
      buf.resize(static_cast{size_t}(len), 0);
      sdl_save_file("glprog" + to_string(prog) + ".bin", buf);
      */
    }
  }
  return prog;
}

