public namespace pgl3d::mainloop "use-unsafe";
public import common -;
public import pointer::raw -;
public import numeric::fpmath -;
public import glm;
public import io::time -;
public import io::environ -;
public import exception -;
public import meta m;
public import text::string::positional -;
public import pgl3d::glgeometry -;
public import pgl3d::scene_node -;
public import pgl3d::pglmotion -;
public import pgl3d::pglbase -;
public import pgl3d::pglcommon -;
public import pgl3d::application -;
public import pgl3d::wfobj -;

private import GL::compat -;
private import pgl3d::glresource -;
private import pgl3d::glshader -;
private import pgl3d::glbuffer -;
private import pgl3d::pglshader -;
private import pgl3d::sdlcontext -;


public function void mainloop(pgl3d_application mutable& app)
{
  GL::base::set_check_error_enabled(1); // FIXME
  try {
    mainloop_internal(app);
  } catch (exception::c_exception ex) {
    sdllog("uncaught exception", ex);
  }
}

public function string sdl_read_file(string mutable& fn)
{
  string r;
  scoped_nullable{SDL_RWclose} rwop;
  rwop.reset(SDL_RWFromFile(fn.c_str(), "rb"));
  if (rwop.get() == rawptr{SDL_RWops}()) {
    throw runtime_error_template{"sdl_read_file"}(fn);
  }
  farray{uchar, 16384} buf;
  while (true) {
    const n = SDL_RWread(rwop, buf.range().to_rawptr().to_voidptr(), 1, 16384);
    if (n == 0) {
      break;
    }
    r.append(buf[0 .. n]);
  }
  return r;
}

struct
frame_statistics()
{
  public metafunction ticks_t farray{uint, 8};
  public uint tprevsec = 0;
  public uint frames = 0;
  public float mutable fps = 0.0;
  public ticks_t ticks;
  public ticks_t ticks_sum;
  public ticks_t ticks_sum_saved;
  init();
  function void init() {
    const t = SDL_GetTicks();
    for (const i, mutable& e : ticks) {
      e = t;
    }
  }
  public function void save_ticks(size_t idx, uint v) {
    const prev_tick = ticks[idx == 0 ? ticks.size() - 1 : idx - 1];
    mutable& cur_tick = ticks[idx];
    cur_tick = v;
    ticks_sum[idx] += cur_tick - prev_tick;
  }
  public function void measure()
  {
    ++frames;
    uint t = SDL_GetTicks();
    if (t - tprevsec >= 1000) {
      const seconds = cast_float(t - tprevsec) / 1000.0;
      fps = cast_float(frames) / seconds;
      ticks_sum_saved = ticks_sum;
      ticks_sum = ticks_t();
      tprevsec = t;
      frames = 0;
    }
  }
}

private function int set_swap_interval(int vsync)
{
  return SDL_GL_SetSwapInterval(vsync);
}

struct
framerate_control(int vsync0)
{
  public int vsync = vsync0;
  public int swap_interval = set_swap_interval(vsync0);
  public ulonglong const timer_res
    = io::system.high_resolution_timer_frequency();
  public double const timer_res_inv = 1.0 / static_cast{double}(timer_res);
  public farray{ulonglong, 12} values;
  public size_t cur = 0;
  public float frame_speed_mul = 1.0;
  public float target_frame_speed_mul = 1.0;
  public function void toggle_vsync()
  {
    vsync = vsync != 0 ? 0 : 1;
    sdllog("vsync=", vsync);
    swap_interval = set_swap_interval(vsync);
  }
  public function void start()
  {
    const t = io::system.high_resolution_timer();
    sdllog("t=", t);
    for (const i, mutable& v: values) {
      v = t;
    }
    cur = 0;
  }
  public function void control()
  {
    const t = io::system.high_resolution_timer();
    // sdllog("t=", t);
    const t0 = values[cur];
    values[cur] = t;
    mutable tdiff = static_cast{double}(t - t0) * timer_res_inv
      / static_cast{double}(values.size());
    if (tdiff < 0.0001 || tdiff > 1.0) {
      tdiff = 1.0 / 60.0;
    }
    target_frame_speed_mul = static_cast{float}(tdiff * 60.0);
    frame_speed_mul = target_frame_speed_mul;
    if (++cur >= values.size()) {
      cur = 0;
    }
  }
}

private function void mainloop_internal(pgl3d_application mutable& app)
{
  sdllog("PGL3D main");

  sdlcontext sdlc;

  // SDL_EventState(SDL_KEYDOWN, 0);
    // FIXME
  SDL_EventState(SDL_KEYUP, 0);
  SDL_EventState(SDL_TEXTEDITING, 0);
  SDL_EventState(SDL_TEXTINPUT, 0);
  if (m::or{is_android, is_ios}) {
    SDL_EventState(SDL_MOUSEBUTTONDOWN, 0);
    SDL_EventState(SDL_MOUSEBUTTONUP, 0);
    SDL_EventState(SDL_MOUSEMOTION, 0);
  }

  sdllog("PGL3D init0");

  shader_config sconf = shader_config(sdlc.gles_flag, sdlc.glmajor,
    sdlc.glminor, sdlc.enable_msaa);
  tree_map{string, ptr{pgldrawer_export_i}} drawers
    = app.on_init_drawers(sconf);
  pglshader pgs = pglshader(sconf, drawers, sdlc.init_w, sdlc.init_h);
  /*
  pglshader pgs = pglshader(sdlc.gles_flag, sdlc.glmajor, sdlc.glminor,
    sdlc.enable_msaa, sdlc.init_w, sdlc.init_h);
  */
  app.on_build_vertices(pgs);
  whole_instances_type local_whole_instances;
  node_factory nfac;
  pgs.prepare(local_whole_instances, nfac);
  /*
  pgs.prepare_vertex_buffers();
  pgs.prepare_whole_instances(local_whole_instances);
  pgs.prepare_node_factory(nfac);
  */

  /* spawn motion thread */
  motion_thread_object mto = motion_thread_object(nfac, local_whole_instances,
    app.get_scene_shared(), pgs.pidft);

  size_t saved_stat_num_objects;

  const test_scroll_mode = false;

  projection_info cam_drawing_frame;

  frame_statistics mutable fr;
  framerate_control mutable frctl = framerate_control(sdlc.vsync);

  int fullscreen = 0;
  function void toggle_fullscreen()
  {
    fullscreen = fullscreen != 0 ? 0 : 1;
    sdllog("fullscreen=", fullscreen);
    SDL_SetWindowFullscreen(sdlc.sdl_window,
      fullscreen != 0 ? SDL_WINDOW_FULLSCREEN_DESKTOP : 0);
  }

  string_map commands;
  function void do_commands()
  {
    if (!commands.empty()) {
      for (const& k, const& v: commands) {
	if (k == "vsync") {
	  frctl.toggle_vsync();
	} else if (k == "fullscreen") {
	  toggle_fullscreen();
	}
      }
      commands.clear();
    }
  }

  bool done = false;
  bool paused = false;
  while (!done) {
    bool scene_next_flag = false;
    SDL_Event ev;
    while (SDL_PollEvent(address(ev)) != 0) {
      const etype = ev.type;
      /*
      sdllog("event", cast_int(ev.type));
       */
      if (etype == SDL_WINDOWEVENT) {
	if (ev.window.event == SDL_WINDOWEVENT_RESIZED) {
	  int w;
	  int h;
	  SDL_GL_GetDrawableSize(sdlc.sdl_window, address(w), address(h));
	  pgs.resize_screen(w, h);
	  frctl.start();
	  sdllog("resized w=", w, " h=", h);
	} else if (ev.window.event == SDL_WINDOWEVENT_HIDDEN) {
	  sdllog("hidden");
	} else if (ev.window.event == SDL_WINDOWEVENT_MINIMIZED ||
	    ev.window.event == SDL_WINDOWEVENT_FOCUS_LOST) {
	  // paused = true;
	  // sdllog("paused");
	} else if (ev.window.event == SDL_WINDOWEVENT_RESTORED ||
	    ev.window.event == SDL_WINDOWEVENT_FOCUS_GAINED) {
	  paused = false;
	  frctl.start();
	  sdllog("restored");
	} else {
	  /*
	  sdllog("window event ", cast_int(ev.window.event));
	  */
	}
      } else if (etype == SDL_QUIT) {
	done = true;
	break;
      }
      /* invoke event handler */
      app.on_event(ev, frctl.frame_speed_mul, pgs.cam, commands);
      do_commands();
    }
    {
      int numkeys;
      const ksp = SDL_GetKeyboardState(address(numkeys));
      const ks = ksp.to_cslice(static_cast{size_t}(numkeys));
      /* invoke key handler */
      app.on_scankeys(ks, frctl.frame_speed_mul, pgs.cam, commands);
      do_commands();
    }
    fr.save_ticks(0, SDL_GetTicks());
    if (paused) {
      SDL_Delay(100);
      fr.tprevsec = SDL_GetTicks();
      continue;
    }
    /* motion and prepare instance data */
    {
      const mess = string_join{" "}(fr.fps, frctl.frame_speed_mul,
	fr.ticks_sum_saved,
	saved_stat_num_objects,
	pgs.cam.camera.angle, pgs.cam.camera.position);
      function void swap_data_cl(motion_thread_shared mutable& mts) {
	// fr.save_ticks(1, SDL_GetTicks());
	cam_drawing_frame = mts.cam;
	mts.cam = pgs.cam;
	mts.frame_speed_mul = frctl.frame_speed_mul;
	mts.message = mess;
	mts.commands.swap(commands);
	local_whole_instances.swap(mts.whole_instances);
	saved_stat_num_objects = mts.stat_num_objects;
      }
      mto.wait_and_exec{swap_data_cl}();
      do_commands();
    }
    fr.save_ticks(1, SDL_GetTicks());
    {
      /* draw frame */
      varray{uint} tick_draw;
      tick_draw = pgs.draw_frame(local_whole_instances, cam_drawing_frame);
      if (tick_draw.size() > 2) {
	fr.save_ticks(2, tick_draw[0]);
	fr.save_ticks(3, tick_draw[1]);
      } else {
	fr.save_ticks(2, SDL_GetTicks());
	fr.save_ticks(3, SDL_GetTicks());
      }
      fr.save_ticks(4, SDL_GetTicks());
      fr.measure();
    }
    fr.save_ticks(5, SDL_GetTicks());
    frctl.control();
    fr.save_ticks(6, SDL_GetTicks());
    SDL_GL_SwapWindow(sdlc.sdl_window);
    fr.save_ticks(7, SDL_GetTicks());
    if (scene_next_flag) {
      frctl.start();
    }
  }
}

