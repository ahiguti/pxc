#!/usr/bin/env pxc
public namespace variant;
import core::common -;
import core::meta::builtin m;
import core::numeric::cast n;

metafunction strlit container::string::strlit;

pure multithreaded union variant {
  unit unitval;
  long longval;
  double doubleval;
  string stringval;
  tptr{iinvoke} tptrval;
}

pure multithreaded interface iinvoke {
  function variant invoke(cstrref const& name, cslice{variant} const& args);
}

pure function {t} variant to_variant(t const& x)
{
  metafunction is_string_or_slice_family{t}
    m::or{
      m::eq{t, string},
      m::eq{t, strlit},
      m::eq{t, strref},
      m::eq{t, cstrref}};
  variant r;
  if (m::eq{t, unit}) {
    r.unitval = unit();
  } else if (n::is_integral{t}) {
    r.longval = n::static_cast{long, t}(x);
  } else if (n::is_fp{t}) {
    r.doubleval = x;
  } else if (is_string_or_slice_family{t}) {
    r.stringval = x;
  } else if (m::eq{t, variant}) {
    r = x;
  } else if (m::eq{t, tptr{iinvoke}}) {
    r.tptrval = x;
  } else {
    // TODO
  }
  return r;
}

pure function {t} t from_variant(variant const& x)
{
  return t(); // TODO
}

pure multithreaded struct foo {
  int val;
  function void add(int x) { val += x; }
  function int get() const { return val; }
}

// TODO: generic
pure multithreaded struct foo_iinvoke : iinvoke : {
  foo value;
  function variant invoke(cstrref const& name, cslice{variant} const& args) {
    variant r;
    if (name == "add") {
      value.add(numeric::cast::static_cast{int, long}(args[0].longval));
    } else if (name == "get") {
      r.longval = value.get();
    }
    return r;
  }
}


pure multithreaded struct {t} stub_iinvoke : iinvoke : {
  t value;
  function variant invoke(cstrref const& name, cslice{variant} const& args) {
    metafunction mfuncstr{t} m::map{m::member_functions{t}, m::to_string};
    metafunction seqstr{n} m::map{m::seq{n}, m::to_string};
    expand(fsym : mfuncstr{t}) {
      if (m::to_string{fsym} == name) {
        metafunction f m::symbol{t, m::to_string{fsym}};
        if (m::eq{m::ret_type{f}, void}) {
          value.fsym(expand(i : seqstr{m::arg_size{f}};
            from_variant{m::arg_type{f, i}}(args[m::to_int{i}])));
          return variant();
        } else {
          return to_variant{m::ret_type{f}}(
            value.fsym(expand(i : seqstr{m::arg_size{f}};
              from_variant{m::arg_type{f, i}}(args[m::to_int{i}]))));
        }
      }
    }
    return variant();
  }
}

function void test1() {
  variant v;
  v.longval = 3;
  v.stringval = "abc";
  println(v);
  v.tptrval = tptr{foo_iinvoke}(foo_iinvoke(foo(99)));
  farray{variant, 1} arg;
  arg[0].longval = 9;
  v.tptrval->invoke("add", arg);
  variant x = v.tptrval->invoke("get", arg[0 .. 0]);
  println(x);
  stub_iinvoke{foo} fi;
}

test1();

