#!/usr/bin/env pxc
namespace variant;
import common -;
import meta::builtin m;
import numeric::cast n;
import exception::builtin -;
import serialization::positional -;
import downcast -;

metafunction strlit container::string::strlit;

multithreaded union variant {
  unit unitval;
  long longval;
  ulong ulongval;
  double doubleval;
  string stringval;
  tptr{iinvoke} tptrval;
}

multithreaded interface iinvoke {
  function variant invoke(cstrref const& name, cslice{variant} const& args);
}

metafunction is_string_or_slice_type{t}
  m::or{
    m::eq{t, string},
    m::eq{t, strlit},
    m::eq{t, strref},
    m::eq{t, cstrref}};

metafunction is_range_type{t}
  m::or{
    m::eq{m::family{t}, "slice"},
    m::eq{m::family{t}, "cslice"},
    m::eq{m::family{t}, "map_range"},
    m::eq{m::family{t}, "map_crange"}};

metafunction to_non_ephemeral{t}
  m::cond{is_string_or_slice_type{t}, string,
  m::cond{is_range_type{t}, m::local{t, "non_ephemeral_type"},
  t}};

threaded function {t} variant to_variant(t const& x)
{
  variant r;
  if (m::eq{t, unit}) {
    r.unitval = unit();
  } else if (n::is_signed_integral{t}) {
    r.longval = n::static_cast{long, t}(x);
  } else if (n::is_unsigned_integral{t}) {
    r.ulongval = n::static_cast{ulong, t}(x);
  } else if (n::is_fp{t}) {
    r.doubleval = x;
  } else if (is_string_or_slice_type{t}) {
    r.stringval = x;
  } else if (m::eq{t, variant}) {
    r = x;
  } else if (m::eq{t, tptr{iinvoke}}) {
    r.tptrval = x;
  } else {
    // TODO
  }
  return r;
}

threaded function {t} t from_variant(variant const& x)
{
  if (m::eq{t, unit}) {
    return unit();
  } else if (n::is_integral{t}) {
    if (case x.longval) {
      return n::static_cast{t, long}(x.longval);
    } else if (case x.ulongval) {
      return n::static_cast{t, ulong}(x.ulongval);
    } else if (case x.doubleval) {
      return n::static_cast{t, double}(x.doubleval);
    } else if (case x.stringval) {
      return decimal_to_integral_nocheck{t}(x.stringval);
    } else if (case x.unitval) {
      return 0;
    }
  } else if (n::is_fp{t}) {
    if (case x.doubleval) {
      return n::static_cast{t, double}(x.doubleval);
    } else if (case x.longval) {
      return n::static_cast{t, long}(x.longval);
    } else if (case x.ulongval) {
      return n::static_cast{t, ulong}(x.ulongval);
    } else if (case x.stringval) {
      return decimal_to_fp_nocheck{t}(x.stringval);
    } else if (case x.unitval) {
      return 0.0;
    }
  } else if (m::eq{t, string}) {
    if (case x.stringval) {
      return x.stringval;
    } else if (case x.longval) {
      return to_string(x.longval);
    } else if (case x.ulongval) {
      return to_string(x.ulongval);
    } else if (case x.doubleval) {
      return to_string(x.doubleval);
    } else if (case x.unitval) {
      return to_string(x.unitval);
    }
  } else if (m::eq{t, variant}) {
    return x;
  } else if (m::eq{t, tptr{iinvoke}}) {
    return r.tptrval;
  }
  throw invalid_field();
}

multithreaded struct foo {
  int val;
  function void add(int x) { val += x; }
  function int get() const { return val; }
  function int func1(int& x, long const& y) {
    x = val;
    val += n::static_cast{int, long}(y);
    return val;
  }
}

multithreaded struct {t} stub_iinvoke(expand(a : m::args{t})) : iinvoke : {
  t value;
  metafunction mfuncstr{t} m::map{m::member_functions{t}, m::to_string};
  metafunction seqstr{n} m::map{m::seq{n}, m::to_string};
  metafunction arg_name{arg} m::concat{"a", m::at{arg, 0}};
  metafunction arg_names{fsym}
    m::map{m::args{m::local{t, m::to_string{fsym}}}, arg_name};
  metafunction argexp{arg}
    m::list{
      arg_name{arg}, // a0, a1, a2, ...
      m::add{m::mul{m::at{arg, 3}, 2}, m::at{arg, 4}}}; // byref * 2 + mutable
  metafunction argexp_list{fsym}
    m::map{m::args{m::local{t, m::to_string{fsym}}}, argexp};
  metafunction args_ae{fsym, ae}
    m::at{m::args{m::local{t, fsym}}, m::to_int{m::substring{ae, 1}}};
  metafunction arg_type_ae{fsym, ae} m::at{args_ae{fsym, ae}, 2};
  metafunction arg_type_noneph{fsym, ae} to_non_ephemeral{arg_type_ae{fsym, ae}};
  function variant invoke(cstrref const& name, cslice{variant} const& args) {
    expand(fsym : mfuncstr{t}) {
      if (m::to_string{fsym} == name) {
	expand(ae : argexp_list{fsym}) {
	  /* const value */
	  arg_type_noneph{fsym, ae} ae = from_variant{arg_type_noneph{fsym, ae}}(
	    args[m::to_int{m::substring{ae, 1}}]);
	  /* mutable value */
	  arg_type_noneph{fsym, ae} ae = from_variant{arg_type_noneph{fsym, ae}}(
	    args[m::to_int{m::substring{ae, 1}}]);
	  /* const reference */
	  arg_type_noneph{fsym, ae} const& ae =
	    downcast_const{stub_iinvoke{arg_type_noneph{fsym, ae}}, iinvoke}(
	      *(args[m::to_int{m::substring{ae, 1}}].tptrval)).value;
	  /* mutable reference */
	  arg_type_noneph{fsym, ae} mutable& ae =
	    downcast_mutable{stub_iinvoke{arg_type_noneph{fsym, ae}}, iinvoke}(
	      *(args[m::to_int{m::substring{ae, 1}}].tptrval)).value;
	}
	if (m::eq{m::ret_type{m::local{t, m::to_string{fsym}}}, void}) {
	  value.fsym(expand(a : arg_names{fsym}; a));
	  return variant();
	} else {
	  return to_variant{m::ret_type{m::local{t, m::to_string{fsym}}}}(
	    value.fsym(expand(a : arg_names{fsym}; a)));
	}
      }
    }
    return variant();
  }
}

function void test1() {
  variant v;
  v.longval = 3;
  v.stringval = "abc";
  println(v);
  metafunction foo_iinvoke stub_iinvoke{foo};
  v.tptrval = tptr{foo_iinvoke}(foo_iinvoke(foo(99)));
  farray{variant, 1} arg;
  arg[0].longval = 9;
  v.tptrval->invoke("add", arg); // 99 + 9
  variant x = v.tptrval->invoke("get", arg[0 .. 0]);
  println(x);
  stub_iinvoke{foo} fi;
}

function void test2() {
  metafunction prefix_a{x} m::concat{"a", x};
  metafunction ss m::map{m::seq{5}, prefix_a};
  metafunction drop_1{x} m::substring{x, 1};
  metafunction sss m::map{ss, drop_1};
  println(m::to_string{sss});
}

function void test3()
{
  stub_iinvoke{foo} f;
  println(m::full_string{to_non_ephemeral{cslice{int}}});
  // dump_arginfo{foo}();
}

test1();
# test2();
test3();

