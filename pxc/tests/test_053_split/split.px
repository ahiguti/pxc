public namespace split;
import common -;
import text::string::serialize -;
import meta m;
import meta::family mf;

public metafunction va_arg_name{i} m::concat{"a", i};
public metafunction va_arg_names{lst} m::map{m::seq{m::size{lst}}, va_arg_name};

public metafunction va_argdecls_const_ref{lst}
  m::map{m::seq{m::size{lst}}, metafunction{x} va_arginfo_const_ref{lst, x}};
public metafunction va_argdecls_mutable_ref{lst}
  m::map{m::seq{m::size{lst}}, metafunction{x} va_arginfo_mutable_ref{lst, x}};

private metafunction va_arginfo_const_ref{lst, i}
  m::list{va_arg_name{i}, m::at0{m::at{lst, i}}, 1, 0};
private metafunction va_arginfo_mutable_ref{lst, i}
  m::list{va_arg_name{i}, m::at0{m::at{lst, i}}, 1, 1};

/* split(str, delim, ret0, ret1, ...) */
public function {lst} bool split(expand(
  m::join{
    m::list{m::list{"str", cstrref, 1, 0}, m::list{"delim", uchar, 0, 0}},
    m::slice{va_argdecls_mutable_ref{lst}, 2}})) 
{
  size_t const sz = str.size();
  size_t curpos = 0;
  expand (arg, idx : m::slice{va_arg_names{lst}, 2}) {
    {
      if (m::ne{idx, 0}) {
	if (curpos == sz) {
	  return false;
	}
	++curpos;
      }
      size_t const dp = find_mapped(str, curpos, delim);
      metafunction telem m::at0{m::at{lst, m::add{idx, 2}}};
      if (mf::is_string_or_slice_type{telem}) {
	arg = str[curpos .. dp];
      } else {
	cstrref s1 = str[curpos .. dp];
	deserialize_from_string_emplace{m::at0{m::at{lst, m::add{idx, 2}}}}(
	  s1, arg);
      }
      curpos = dp;
    }
  }
  return true;
}

