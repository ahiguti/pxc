public namespace enummeta2;
import core::common -;
import core::meta m;

enum foo {
  foo1 = 1,
  foo2 = 2,
  foo3 = 3,
  foo4 = 4,
}

bitmask bar {
  bar01 = 1,
  bar02 = 2,
  bar04 = 4,
  bar08 = 8,
  bar10 = 0x10,
  bar20 = 0x20,
  bar40 = 0x40,
}

function {t} string enum_to_string(t v)
{
  metafunction vals m::values{t};
  expand (i : m::seq{m::size{vals}}) {
    if (v == m::at{vals, i}) {
      return m::full_string{m::at{vals, i}};
    }
  }
  return "?";
}

function {t} string bitmask_to_string(t v)
{
  string s;
  if (v == 0) {
    return "0";
  }
  metafunction vals m::values{t};
  expand (i : m::seq{m::size{vals}}) {
    if ((v & m::at{vals, i}) != 0) {
      if (!s.empty()) {
        s.append("|");
      }
      s.append(m::full_string{m::at{vals, i}});
    }
  }
  return s;
}

function void test1() {
  foo f = foo2;
  println(enum_to_string(f));
  bar b = bar04 | bar20;
  println(bitmask_to_string(b));
}

test1();

