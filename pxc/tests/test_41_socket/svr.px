namespace svr;
import common -;
import io -;
import io::file -;
import io::errno -;
import io::standard -;
import io::addrinfo -;
import io::socket -;
import io::signal -;
import thread -;

multithreaded struct server_thread(io const& i, tptr{bool} const& rp,
  tptr{string} const& s)
{
  io const iop = i;
  tptr{bool} readyp = rp;
  tptr{string} str_r = s;
  function void notify()
  {
    var lck = lock_guard{bool}(readyp);
    *lck = true;
    lck.notify_one();
  }
  function void main()
  {
    try {
      file_println(get_stderr(iop), "svr");
      var sf = create_acceptor(iop, "localhost", "9999", AF_INET, SOCK_STREAM,
	1024, 0); // TODO: fix EADDRINUSE
      file_println(get_stderr(iop), "accepted");
      notify();
      var peer_addr = sockaddr_storage();
      var sock = accept(sf.value, peer_addr);
      string s;
      size_t len = 10;
      sock.value.read(s, len);
      *str_r = s;
    } catch (exception::c_exception e) {
      file_println(get_stderr(iop), "thrown");
      notify();
    } catch (io::errno::errno_t e) {
      file_println(get_stderr(iop), "thrown errno");
      notify();
    }
  }
}

multithreaded struct client_thread(io const& i, tptr{bool} const& rp)
{
  io const iop = i;
  tptr{bool} readyp = rp;
  function void main()
  {
    {
      var lck = lock_cguard{bool}(readyp);
      while (true) {
	if (*lck) { break; }
	lck.wait();
      }
    }
    file_println(get_stderr(iop), "got notification");
    var sf = create_connector(iop, "localhost", "9999", AF_INET, SOCK_STREAM,
      10);
    var sock = connect(sf.value);
    size_t wlen = 0;
    sock.value.write("hoge", wlen);
  }
}

function void t1()
{
  signal(io::system, SIGPIPE, SIG_IGN);
  var readyp = to_tptr(bool(false));
  var strp = to_tptr(string());
  var sthr = create_thread(server_thread(io::system, readyp, strp));
  var cthr = create_thread(client_thread(io::system, readyp));
  sthr.join();
  cthr.join();
  println(*strp);
}

t1();

