public namespace recobj "export-unsafe";
import common -;
import pointer::raw -;
import numeric::fpmath -;
import SDL2 -;
import SDL2::Image -;
import SDL2::TTF -;
import GL::gles2 -;
import GL::glm glm;
import io::time -;
import io::environ -;
import exception -;
import meta m;

import glshader -;
import gldraw -;
import glgeometry -;

valuetype struct main_uniforms
{
  public glm::mat4 view_projection_matrix;
  public glm::mat4 model_matrix;
  public glm::mat4 normal_matrix;
  public int sampler;
  public glm::vec3 camera_pos;
  public glm::vec3 light_dir;
  public glm::vec3 view_dir;
}

valuetype struct main_vertex_attributes
{
  public glm::vec3 position;
  public glm::vec3 normal;
  public glm::vec4 tangent;
  public glm::vec2 uv;
}

valuetype struct font_uniforms
{
  public int sampler;
}

valuetype struct font_vertex_attributes
{
  public glm::vec2 pos;
  public glm::vec2 uvpos;
}

metafunction main_shader shader{main_uniforms, main_vertex_attributes};
metafunction main_shader_ptr cptr{main_shader};
metafunction font_shader shader{font_uniforms, font_vertex_attributes};
metafunction font_shader_ptr cptr{font_shader};

valuetype struct main_texture_info
{
  public uint surface_w;
  public uint surface_h;
  public uint main_texture_id;
}

valuetype struct font_texture_info
{
  public varray{uint} char_x;
  public uint char_h;
  public uint surface_w;
  public uint surface_h;
  public uint font_texture_id;
}

valuetype struct camera_info
{
  public glm::mat4 projection;
  public glm::quat camera_angle;
  public glm::vec3 camera_pos;
  public int screen_width;
  public int screen_height;
}

function void build_vertices(buffer_object{main_shader} mutable& bo)
{
  metafunction tattr m::symbol{main_shader, "vertex_attributes_type"};
  metafunction fill_v fill{tattr};
  metafunction edge_v edge{tattr};
  /* vtx0 */
  {
    bo.vs_arr.push_back(vertex_set{tattr}());
    const vtx0 = bo.vs_arr[bo.vs_arr.size() - 1].vtxs;
    const t0 = fill_v(vtx0);
    regular_polygon(0.3, 100,
      bump(0.04, 0.5, 1, t0, t0),
      bump(0.01, 0.9, 5,
	sphere(10, t0),
	t0))
      (cslice{glm::vec3}());
  }
  /* vtx1 */
  {
    bo.vs_arr.push_back(vertex_set{tattr}());
    const vtx1 = bo.vs_arr[bo.vs_arr.size() - 1].vtxs;
    const t1 = fill_v(vtx1);
    regular_polygon(0.1, 4,
      bump(8.0, 1.0, 1, t1, t1),
      t1)
      (cslice{glm::vec3}());
  }
  /* vtx2 */
  {
    bo.vs_arr.push_back(vertex_set{tattr}());
    const vtx2 = bo.vs_arr[bo.vs_arr.size() - 1].vtxs;
    const t2 = fill_v(vtx2);
    regular_polygon(10.0, 100,
      bump(1.0, 1.0, 1, hole(), edge_v(vtx2, 0, hole())),
      hole())
      (cslice{glm::vec3}());
  }
  /* vtx3 */
  {
    bo.vs_arr.push_back(vertex_set{tattr}());
    const vtx3 = bo.vs_arr[bo.vs_arr.size() - 1].vtxs;
    const t3 = fill_v(vtx3);
    regular_polygon(2.0, 4,
      bump(2.828, 1.0, 1, t3, t3),
      t3)
      (cslice{glm::vec3}());
  }
  for (const i, const& a: bo.vs_arr) {
    println(i, "vtxs", a.vtxs->vertex_values.size(), "elems",
      a.vtxs->elements.size());
  }
}

function void draw_frame(camera_info mutable& cam, main_shader_ptr const& sdr,
  buffer_object{main_shader} const& bo, font_shader_ptr const& psdr,
  font_texture_info const& fti, cstrref const& mess)
{
  /* main */
  glUseProgram(sdr->program);
  check_error();
  glEnable(GL_CULL_FACE);
  glEnable(GL_DEPTH_TEST);
  glDisable(GL_BLEND);
  glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);
  const light = glm::make_vec3(5.0, 5.0, -5.0).normalize();
  glUniform3fv(sdr->u_light_dir, 1, light.crawptr());
  check_error();
  glUniform3fv(sdr->u_view_dir, 1,
    (cam.camera_angle.to_mat3() * glm::make_vec3(0.0, 0.0, -1.0)).crawptr());
  check_error();
  glUniform3fv(sdr->u_camera_pos, 1, cam.camera_pos.crawptr());
  const view = (glm::mat4().translate(cam.camera_pos)
    * cam.camera_angle.to_mat4())
    .inverse();
  const vp = cam.projection * view;
  glUniformMatrix4fv(sdr->u_view_projection_matrix, 1, 0, vp.crawptr());
  const red = make_farray{float}(0.8, 0.1, 0.0, 1.0);
  glUniform1i(sdr->u_sampler, 1);
  const sn = bo.instantiate_scene(2);
  const modelview_cur = glm::mat4();
  function void set_uniform_cb(glm::mat4 const& model_mat)
  {
    glUniformMatrix4fv(sdr->u_model_matrix, 1, 0, model_mat.crawptr());
    const nm = model_mat.to_3();
    glUniformMatrix3fv(sdr->u_normal_matrix, 1, 0, nm.crawptr());
  }
  sn->draw{set_uniform_cb, main_shader}(bo, modelview_cur, *sdr);
  check_error();
  /* panel */
  glUseProgram(psdr->program);
  check_error();
  glDisable(GL_CULL_FACE);
  check_error();
  glDisable(GL_DEPTH_TEST);
  check_error();
  glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);
  check_error();
  glEnable(GL_BLEND);
  check_error();
  glUniform1i(psdr->u_sampler, 0);
  check_error();
  const lpp = static_cast{uint}(psdr->a_pos);
  const lpu = static_cast{uint}(psdr->a_uvpos);
  glEnableVertexAttribArray(lpp);
  check_error();
  glEnableVertexAttribArray(lpu);
  check_error();
  varray{float} pvals;
  varray{float} uvals;
  uint cur_x = 0;
  uint cur_y = 0;
  for (size_t i, const ch: mess) {
    if (ch < 0x20) {
      continue;
    }
    const tx0 = (ch > 0x20) ? fti.char_x[ch - 0x20 - 1] : 0;
    const tx1 = fti.char_x[ch - 0x20];
    const ty0 = fti.char_h;
    const ty1 = 0;
    const tw = tx1 - tx0;
    const th = fti.char_h;
    const x0 = static_cast{float}(cur_x) * 2.0f
      / static_cast{float}(cam.screen_width) - 1.0f;
    const y0 = static_cast{float}(cur_y) * 2.0f
      / static_cast{float}(cam.screen_height) - 1.0f;
    const x1 = static_cast{float}(cur_x + tw) * 2.0f
      / static_cast{float}(cam.screen_width) - 1.0f;
    const y1 = static_cast{float}(cur_y + th) * 2.0f
      / static_cast{float}(cam.screen_height) - 1.0f;
    const u0 = static_cast{float}(tx0) / static_cast{float}(fti.surface_w);
    const v0 = static_cast{float}(ty0) / static_cast{float}(fti.surface_h);
    const u1 = static_cast{float}(tx1) / static_cast{float}(fti.surface_w);
    const v1 = 0.0f;
    /* println(x0, y0, x1, y1, u0, v0, u1, v1); */
    pvals.push_back(x0);
    pvals.push_back(y0);
    pvals.push_back(x0);
    pvals.push_back(y1);
    pvals.push_back(x1);
    pvals.push_back(y0);
    pvals.push_back(x1);
    pvals.push_back(y0);
    pvals.push_back(x0);
    pvals.push_back(y1);
    pvals.push_back(x1);
    pvals.push_back(y1);
    uvals.push_back(u0);
    uvals.push_back(v0);
    uvals.push_back(u0);
    uvals.push_back(v1);
    uvals.push_back(u1);
    uvals.push_back(v0);
    uvals.push_back(u1);
    uvals.push_back(v0);
    uvals.push_back(u0);
    uvals.push_back(v1);
    uvals.push_back(u1);
    uvals.push_back(v1);
    cur_x += tw;
  }
  if (pvals.size() > 0) {
    glVertexAttribPointer(lpp, 2, GL_FLOAT, 0, 0, pvals.crawptr().void());
    check_error();
    glVertexAttribPointer(lpu, 2, GL_FLOAT, 0, 0, uvals.crawptr().void());
    check_error();
    glDrawArrays(GL_TRIANGLES, 0, static_cast{int}(pvals.size() / 2));
    check_error();
    glDisableVertexAttribArray(lpp);
    check_error();
    glDisableVertexAttribArray(lpu);
    check_error();
  }
}

function void reshape(camera_info mutable& cam, int width, int height)
{ 
  cam.screen_width = width;
  cam.screen_height = height;
  update_mvp(cam);
}

function void update_mvp(camera_info mutable& cam)
{
  float const perspective_distance = 1.1;
  float const min_distance = 1.0;
  const h = static_cast{float}(cam.screen_height)
    / static_cast{float}(cam.screen_width);
  glViewport(0, 0, static_cast{GLsizei}(cam.screen_width),
    static_cast{GLsizei}(cam.screen_height));
  const d = static_cast{float}(perspective_distance);
  const near = static_cast{float}(d - min_distance);
  const far = static_cast{float}(d + min_distance + 100.0);
  const left = static_cast{float}(-1.0 * ((d - min_distance) / d));
  const right = static_cast{float}(1.0 * ((d - min_distance) / d));
  const bottom = static_cast{float}(-h * ((d - min_distance) / d));
  const top = static_cast{float}(h * ((d - min_distance) / d));
  cam.projection = glm::frustum{float}(left, right, bottom, top, near, far);
}

threaded function {t} t power_of_2(t v)
{
  mutable t r = static_cast{t}(1);
  while (r != 0 && r < v) { r <<= 1; }
  return r;
}

metafunction gl_precision m::cond{m::eq{m::profile{"platform"}, "ios"},
  "precision highp float;\n", ""};

function main_shader_ptr
init_main_shader()
{
  const vert = m::concat{
    gl_precision,
    "uniform mat4 view_projection_matrix;\n",
    "uniform mat4 model_matrix;\n",
    "uniform mat3 normal_matrix;\n",
    "attribute vec3 position;\n",
    "attribute vec3 normal;\n",
    "attribute vec4 tangent;\n",
    "attribute vec2 uv;\n",
    "varying vec3 vary_position;\n",
    "varying vec3 vary_normal;\n",
    "varying vec3 vary_tangent;\n",
    "varying vec3 vary_binormal;\n",
    "varying vec2 vary_uv;\n",
    "void main(void) {\n",
    "  vec4 pos4 = vec4(position, 1.0);\n",
    "  gl_Position = view_projection_matrix * model_matrix * pos4;\n",
    "  vary_position = (model_matrix * pos4).xyz;\n",
    "  vary_normal = normal_matrix * normal;\n",
    "  vary_tangent = normal_matrix * tangent.xyz;\n",
    "  vary_binormal = cross(vary_normal, vary_tangent) * tangent.w;\n",
    "  vary_uv = uv;\n",
    "}\n"};
  const frag = m::concat{
    gl_precision,
    "uniform sampler2D sampler;\n",
    "uniform vec3 camera_pos;\n",
    "uniform vec3 light_dir;\n",
    "uniform vec3 view_dir;\n",
    "varying vec3 vary_position;\n",
    "varying vec3 vary_normal;\n",
    "varying vec3 vary_tangent;\n",
    "varying vec3 vary_binormal;\n",
    "varying vec2 vary_uv;\n",
    "void main(void) {\n",
    "  vec3 nor = vary_normal;\n",
    "  float u = vary_uv.x;\n",
    "  float v = vary_uv.y;\n",
    "  float u1 = (u + 0.5) * 128.0;\n",
    "  float v1 = (v + 0.5) * 128.0;\n",
    "  float ui = floor(u1);\n",
    "  float uj = u1 - ui;\n",
    "  float vi = floor(v1);\n",
    "  float vj = v1 - vi;\n",
    "  vec4 c0 = texture2D(sampler, vec2(ui / 128.0, vi / 128.0));\n",
    "  int alv = int(c0.a * 255.0 + 0.5);\n",
    "  c0 *= 0.1;\n",
    "  int left_top = int(vj - uj >= 0.0);\n",
    "  int left_bottom  = int(vj + uj <= 1.0);\n",
    "  int ut1 = 0;\n",
    "  int vt1 = 0;\n",
    "  ut1 += int(alv == 4) - int(alv == 8);\n",
    "  ut1 += int(alv == 3) * (1 - left_bottom);\n",
    "  ut1 += int(alv == 5) * (1 - left_top);\n",
    "  ut1 += -int(alv == 7) * left_bottom;\n",
    "  ut1 += -int(alv == 1) * left_top;\n",
    "  vt1 += int(alv == 6) - int(alv == 2);\n",
    "  vt1 += int(alv == 5) * left_top;\n",
    "  vt1 += int(alv == 7) * (1 - left_bottom);\n",
    "  vt1 += -int(alv == 1) * (1 - left_top);\n",
    "  vt1 += -int(alv == 3) * left_bottom;\n",
    "  nor += float(ut1) * vary_tangent * 0.3;\n",
    "  nor += float(vt1) * vary_binormal * 0.3;\n",
    "  nor = normalize(nor);\n",
    /* */
    "  float brightness = max(dot(light_dir, nor), 0.0) * 0.5 + 0.1;\n",
    "  vec3 reflection_vec = reflect(-light_dir, nor);\n",
    "  vec3 vtx_from_camera = normalize(vary_position - camera_pos);\n",
    "  float cos_angle = max(0.0, dot(vtx_from_camera, reflection_vec));\n",
    "  float specular = pow(cos_angle, 32.0);\n",
    "  vec4 color1 = vec4(0.25, 0.26, 0.27, 1.0);\n",
    "  vec4 color2 = vec4(1.0, 1.0, 1.0, 1.0);\n",
    "  vec4 color = color1 * brightness + color2 * specular;\n",
    "  color = c0 + color;\n",
    /* */
    "  gl_FragColor = color;\n",
    "}\n"};
  return make_shader_ptr{main_uniforms, main_vertex_attributes}(vert, frag);
}

function font_shader_ptr
init_font_shader()
{
  const vert = m::concat{
    gl_precision,
    "attribute vec2 pos;\n",
    "attribute vec2 uvpos;\n",
    "varying vec2 texcoord;\n",
    "void main(void) {\n",
    "  gl_Position = vec4(pos, 0.0, 1.0);\n",
    "  texcoord = uvpos;\n",
    "}\n"
  };
  const frag = m::concat{
    gl_precision,
    "uniform sampler2D sampler;\n",
    "varying vec2 texcoord;\n",
    "void main(void) {\n",
    "  vec4 col = texture2D(sampler, texcoord);\n",
    "  gl_FragColor = col;\n",
    "}\n"};
  return make_shader_ptr{font_uniforms, font_vertex_attributes}(vert, frag);
}

function void
init_texture(main_texture_info mutable& mti, font_texture_info mutable& fti)
{
  {
    /* font */
    const fnames = make_farray{string}(
      "LiberationMono-Bold.ttf",
      "/Library/Fonts/Osaka.ttf",
      "/usr/share/fonts/liberation/LiberationMono-Bold.ttf");
    option{ptr{TTF_Font}} font;
    for (size_t i, const& fn: fnames) {
      font = TTF_OpenFont(fn, 16);
      if (case font.some) {
	break;
      }
    }
    SDL_Color mutable fg;
    fg.r = 255;
    fg.g = 255;
    fg.b = 255;
    fg.a = 255;
    uint hmax = 0;
    uint wtotal = 0;
    varray{ptr{SDL_Surface}} sur_arr;
    for (const i: 0 .. 0x60) {
      farray{uchar, 1} z;
      z[0] = static_cast{uchar}(i + 0x20);
      const sur = TTF_RenderUTF8_Blended(font.some, z, fg).some;
      /* println(z, sur.W(), sur.H()); */
      sur_arr.push_back(sur);
      hmax = max(hmax, sur.H());
      wtotal += sur.W();
    }
    const sur_chars = SDL_CreateRGBSurface(
      static_cast{int}(power_of_2(wtotal)),
      static_cast{int}(power_of_2(hmax)), 32, 0, 0, 0, 0).some;
    println("wtotal, hmax", wtotal, hmax);
    fti.char_x.clear();
    uint xcur = 0;
    for (const i, const e: sur_arr) {
      SDL_Rect r0;
      SDL_Rect r1;
      r0.x = 0;
      r0.y = 0;
      r0.w = static_cast{int}(e.W());
      r0.h = static_cast{int}(e.H());
      r1.x = static_cast{int}(xcur);
      r1.y = 0;
      r1.w = r0.w;
      r1.h = r1.h;
      const v = SDL_BlitSurface(e, r0, sur_chars, r1);
      xcur += static_cast{uint}(r1.w);
      fti.char_x.push_back(xcur);
    }
    fti.char_h = hmax;
    fti.surface_w = sur_chars.W();
    fti.surface_h = sur_chars.H();
    const sur = sur_chars;
    const sur_chars_buf = sur_chars.Pixels();
    for (const x: 0 .. sur_chars.W()) {
      for (const y: 0 .. sur_chars.H()) {
	const i = (y * sur_chars.W() + x) * 4;
	sur_chars_buf[i+0] = 255;
	sur_chars_buf[i+1] = 255;
	sur_chars_buf[i+2] = 255;
      }
    }
    /* texture */
    glActiveTexture(GL_TEXTURE0);
    check_error();
    glGenTextures(1, address(fti.font_texture_id));
    check_error();
    glBindTexture(GL_TEXTURE_2D, fti.font_texture_id);
    check_error();
    glTexImage2D(GL_TEXTURE_2D, 0, static_cast{int}(GL_RGBA),
      static_cast{int}(sur.W()), static_cast{int}(sur.H()),
      0, GL_RGBA, GL_UNSIGNED_BYTE, sur.Pixels().crawptr().void());
    check_error();
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER,
      static_cast{int}(GL_NEAREST));
    check_error();
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER,
      static_cast{int}(GL_NEAREST));
    check_error();
  }
  {
    // const sur = IMG_Load("img-c1024.jpg").some;
    const sur0 = IMG_Load("test.bmp").some;
    const bpp = sur0.BytesPerPixel();
    if (bpp != 4 && bpp != 3) {
      throw runtime_error_template{"img_load"}("1");
    }
    const sur0_w = sur0.W();
    const sur0_h = sur0.H();
    const sur1 = SDL_CreateRGBSurface(
      static_cast{int}(power_of_2(sur0_w)),
      static_cast{int}(power_of_2(sur0_h)), 32, 0, 0, 0, 0).some;
    const sur1_w = sur1.W();	
    const sur1_h = sur1.H();	
    const sur0buf = sur0.Pixels();
    const sur1buf = sur1.Pixels();
    for (const x: 0 .. sur0_w) {
      for (const y: 0 .. sur0_h) {
	const mx = sur0_w - x - 1;
	const my = sur0_h - y - 1;
	uchar alpha_value = 0;
	const uint brd = 6;
	if (x == y && x < brd) {
	  alpha_value = 1;
	} else if (y < brd && y < x && y < mx) {
	  alpha_value = 2;
	} else if (mx == y && mx < brd) {
	  alpha_value = 3;
	} else if (mx < brd && mx < y && mx < my) {
	  alpha_value = 4;
	} else if (mx < brd && mx == my) {
	  alpha_value = 5;
	} else if (my < brd && my < x && my < mx) {
	  alpha_value = 6;
	} else if (my == x && my < brd) {
	  alpha_value = 7;
	} else if (x < brd && x < y && x < my) {
	  alpha_value = 8;
	}
	const offset0 = (y * sur0_w + x) * bpp;
	const offset1 = (y * sur1_w + x) * 4;
	sur1buf[offset1+0] = sur0buf[offset0+0];
	sur1buf[offset1+1] = sur0buf[offset0+1];
	sur1buf[offset1+2] = sur0buf[offset0+2];
	sur1buf[offset1+3] = alpha_value;
      }
    }
    const mode = GL_RGBA;
    glActiveTexture(GL_TEXTURE1);
    check_error();
    glGenTextures(1, address(mti.main_texture_id));
    check_error();
    glBindTexture(GL_TEXTURE_2D, mti.main_texture_id);
    check_error();
    glTexImage2D(GL_TEXTURE_2D, 0, static_cast{int}(mode),
      static_cast{int}(sur1.W()), static_cast{int}(sur1.H()),
      0, mode, GL_UNSIGNED_BYTE, sur1.Pixels().crawptr().void());
    check_error();
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER,
      static_cast{int}(GL_NEAREST));
    check_error();
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER,
      static_cast{int}(GL_NEAREST));
    check_error();
    mti.surface_w = sur1.W();
    mti.surface_h = sur1.H();
  }
}

function void check_error()
{
  const v = glGetError();
  if (v == GL_NO_ERROR) {
    return;
  }
  sdllog(string_join{""}("error: ", static_cast{int}(v)));
  throw runtime_error_template{"gl_error"}("1");
}

function void sdllog(cstrref const& mess)
{
  SDL_LogWarn(SDL_LOG_CATEGORY_TEST, mess);
}

{
  camera_info cam;
  sdllog("PXC-SDL2 main");
  const ini = SDL_Init(SDL_INIT_VIDEO);
  int init_w = 800;
  int init_h = 800;
  const num_modes = SDL_GetNumDisplayModes(0);
  for (int i: 0 .. num_modes) {
    SDL_DisplayMode mode;
    SDL_GetDisplayMode(0, i, mode);
    if (mode.w * mode.h > init_w * init_h) {
      init_w = mode.w;
      init_h = mode.h;
    }
    sdllog(string_join{" "}("displaymode", mode.w, mode.h));
  }
  SDL_GL_SetAttribute(SDL_GL_DOUBLEBUFFER, 1);
  SDL_GL_SetAttribute(SDL_GL_CONTEXT_MAJOR_VERSION, 2);
  SDL_GL_SetAttribute(SDL_GL_CONTEXT_MINOR_VERSION, 0);
  mutable wflags = SDL_WINDOW_OPENGL | SDL_WINDOW_RESIZABLE
    | SDL_WINDOW_ALLOW_HIGHDPI;
  /*
  wflags |= SDL_WINDOW_FULLSCREEN;
   */
  if (get_option("-f") == "1") { wflags |= SDL_WINDOW_FULLSCREEN; }
  mutable vsync = 1;
  if (get_option("-v") == "1") { vsync = 0; }

  sdllog(string_join{" "}("create_window", init_w, init_h));
  const wnd = SDL_CreateWindow("PXC-SDL2", 0, 0, init_w, init_h, wflags).some;
  SDL_GL_GetDrawableSize(wnd, init_w, init_h);
  const glctx = SDL_GL_CreateContext(wnd);
  SDL_GL_SetSwapInterval(vsync);

  sdllog("PXC-SDL2 init0");
  const sdr = init_main_shader();
  const psdr = init_font_shader();
  main_texture_info mti;
  font_texture_info fti;
  init_texture(mti, fti);
  sdllog("PXC-SDL2 initialized");

  sdllog("PXC-SDL2 reshape");
  reshape(cam, init_w, init_h);

  uint tprevsec = 0;
  uint tprev = SDL_GetTicks();
  uint frames = 0;
  float mutable fps = 0.0;
  cam.camera_angle = glm::make_quat_wxyz(1.0, 0.0, 0.0, 0.0);
  cam.camera_pos = glm::make_vec3(0.0, 0.0, 10.0);

  farray{uint, 2} mutable frame_tdiffs;
  for (const i, mutable& e: frame_tdiffs) {
    e = 1000U / static_cast{uint}(frame_tdiffs.size());
  }
  float mutable frame_speed_mul = 1.0f;

  metafunction ticks_t farray{uint, 5};
  ticks_t ticks;
  ticks_t ticks_sum;
  ticks_t ticks_sum_prev;
  for (const i, mutable& e : ticks) {
    e = tprev;
  }
  function void save_ticks(size_t idx) {
    const prev_tick = ticks[idx == 0 ? ticks.size() - 1 : idx - 1];
    mutable& cur_tick = ticks[idx];
    cur_tick = SDL_GetTicks();
    ticks_sum[idx] += cur_tick - prev_tick;
  }

  buffer_object{main_shader} bo;
  {
    const t0 = system.gettimeofday_double();
    build_vertices(bo);
    const t1 = system.gettimeofday_double();
    bo.create_vbo();
    const t2 = system.gettimeofday_double();
    SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
      string_join{" "}("build vertex", t1 - t0, t2 - t1));
  }

  bool done = false;
  bool paused = false;
  function void camera_rotate(float x, float y, float z, float angle) {
    const qq = glm::make_quat_angle_axis(angle * frame_speed_mul, x, y, z);
    cam.camera_angle = (cam.camera_angle * qq).normalize();
  }
  function void camera_move(float x, float y, float z) {
    cam.camera_pos += cam.camera_angle.to_mat3() * glm::make_vec3(x, y, z)
      * frame_speed_mul;
  }
  varray{SDL_FingerID} fingerids;
  farray{bool, 2} fingdown;
  farray{size_t, 2} fingerdown_time;
  farray{glm::vec2, 2} start_pos;
  farray{glm::vec2, 2} cur_pos;
  glm::vec2 finger_xyrotate_delta;
  glm::vec2 finger_xymove_delta;
  float finger_zrotate_delta;
  float finger_zmove_delta;
  int finger_zmove_mode = 0;
  function void finger_motion(float x, float y, float dx, float dy,
    SDL_FingerID fng) {
    size_t fid = 2;
    if (fingerids.size() > 0 && (fingerids[0] == fng || fingerids[0] == 0)) {
      fid = 0;
    } else if (fingerids.size() > 1 && fingerids[1] == fng) {
      fid = 1;
    }
    if (fid < 2) {
      cur_pos[fid] = glm::make_vec2(x, y);
      /*
      SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	  string_join{" "}("finger_motion", x, y, fng));
      */
    }
  }
  function void finger_down(float x, float y, SDL_FingerID fng) {
    size_t fid = 2;
    if (fingerids.size() > 0 && (fingerids[0] == fng || fingerids[0] == 0)) {
      fid = 0;
    } else if (fingerids.size() > 1 && fingerids[1] == fng) {
      fid = 1;
    } else if (fingerids.size() < 2) {
      fid = fingerids.size();
      fingerids.push_back(fng);
    }
    if (fid < 2) {
      fingdown[fid] = true;
      start_pos[fid] = glm::make_vec2(x, y);
      cur_pos[fid] = start_pos[fid];
      /*
      SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	  string_join{" "}("finger_down", x, y, fid));
      */
      fingerdown_time[0] = 0;
      fingerdown_time[1] = 0;
      finger_zmove_mode = 0;
    }
    /*
    SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	string_join{" "}("finger_down_post", fingerids));
    */
  }
  function void finger_up(SDL_FingerID fng) {
    size_t fid = 2;
    if (fingerids.size() > 0 && (fingerids[0] == fng || fingerids[0] == 0)) {
      fid = 0;
      fingerids.erase(0, 1);
    } else if (fingerids.size() > 1 && fingerids[1] == fng) {
      fid = 1;
      fingerids.resize(1, 0);
    }
    if (fid < 2) {
      fingdown[fid] = false;
      /*
      SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	  string_join{" "}("finger_up", fid));
      */
      start_pos = cur_pos;
      fingerdown_time[0] = 0;
      fingerdown_time[1] = 0;
      finger_zmove_mode = 0;
    }
    /*
    SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	string_join{" "}("finger_up_post", fingerids));
    */
  }
  function void finger_action_one_finger(size_t fid) {
    mutable dx = (cur_pos[fid].x - start_pos[fid].x);
    mutable dy = (cur_pos[fid].y - start_pos[fid].y);
    const d = glm::distance(start_pos[fid], cur_pos[fid]);
    if (d > 3.0) {
      dx /= d / 3.0;
      dy /= d / 3.0;
    }
    /*
    sdllog(string_join{" "}("onefinger", dx, dy));
    */
    camera_rotate(1.0, 0.0, 0.0, -dy * 2.0);
    camera_rotate(0.0, 1.0, 0.0, -dx * 2.0);
  }
  function void finger_action_two_fingers() {
    const d0 = cur_pos[0] - start_pos[0];
    const d1 = cur_pos[1] - start_pos[1];
    if (glm::dot(d0, d1) < 0.0f) {
      /* z move */
      if (finger_zmove_mode != 2) {
	const distance_start = glm::distance(start_pos[0], start_pos[1]);
	mutable distance_cur = glm::distance(cur_pos[0], cur_pos[1]);
	if (distance_cur <= 0.0001f) {
	  distance_cur = 0.0001f;
	}
	const rat = distance_start / distance_cur;
	const prev = finger_zmove_delta;
	if (rat > 1.2) {
	  finger_zmove_delta += 0.0001;
	  if (finger_zmove_delta >= 0.01) {
	    finger_zmove_delta = 0.01;
	  }
	  finger_zmove_mode = 1;
	} else if (rat < 0.8) {
	  finger_zmove_delta -= 0.0001;
	  if (finger_zmove_delta <= -0.01) {
	    finger_zmove_delta = -0.01;
	  }
	  finger_zmove_mode = 1;
	}
	if ((prev < 0.0f && finger_zmove_delta > 0.0f) ||
	  (prev > 0.0f && finger_zmove_delta < 0.0f)) {
	  /* stop */
	  finger_zmove_delta = 0.0f;
	  finger_zmove_mode = 2;
	}
      }
    } else {
      /* xy move */
      const d0 = cur_pos[0] - start_pos[0];
      const d1 = cur_pos[1] - start_pos[1];
      mutable avg = (d0 + d1) / 2.0f;
      const d = (glm::distance(start_pos[0], cur_pos[0]),
	glm::distance(start_pos[1], cur_pos[0])) / 2.0f;
      if (d > 3.0) {
	avg /= d / 3.0;
      }
      avg /= 32.0f;
      camera_move(avg.x, -avg.y, 0.0);
    }
  }
  function void finger_action_step() {
    /*
    sdllog(string_join{" "}("finger_action_step", fingdown[0], fingdown[1]));
    */
    if (fingdown[0] && !fingdown[1]) {
      if (fingerdown_time[0] >= 2) {
	finger_action_one_finger(0);
      } else {
	start_pos[0] = (start_pos[0] + cur_pos[0]) / 2.0f;
	fingerdown_time[0] += 1;
      }
      if (fingerids[0] == 0) {
	/* mouse button */
	finger_zmove_delta -= 0.0001;
	if (finger_zmove_delta <= -0.01) {
	  finger_zmove_delta = -0.01;
	}
      }
    } else if (!fingdown[0] && fingdown[1]) {
      if (fingerdown_time[1] >= 2) {
	finger_action_one_finger(0);
      } else {
	start_pos[1] = (start_pos[1] + cur_pos[1]) / 2.0f;
	fingerdown_time[1] += 1;
      }
    } else if (fingdown[0] && fingdown[1]) {
      if (fingerdown_time[0] >= 2 && fingerdown_time[1] >= 2) {
	finger_action_two_fingers();
      } else {
	start_pos[0] = (start_pos[0] + cur_pos[0]) / 2.0f;
	fingerdown_time[0] += 1;
	start_pos[1] = (start_pos[1] + cur_pos[1]) / 2.0f;
	fingerdown_time[1] += 1;
      }
    }
    camera_move(0.0, 0.0, finger_zmove_delta);
  }
  while (!done) {
    save_ticks(0);
    SDL_Event ev;
    while (SDL_PollEvent(ev)) {
      const etype = ev.type;
      /*
       sdllog(string_join{" "}("event", static_cast{int}(ev.type)));
       */
      if (etype == SDL_WINDOWEVENT) {
	if (ev.window.event == SDL_WINDOWEVENT_RESIZED) {
	  int w;
	  int h;
	  SDL_GL_GetDrawableSize(wnd, w, h);
	  reshape(cam, w, h);
	  SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	    string_join{""}("resized w=", w, " h=", h));
	} else if (ev.window.event == SDL_WINDOWEVENT_HIDDEN) {
	  /* sdllog("hidden"); */
	} else if (ev.window.event == SDL_WINDOWEVENT_MINIMIZED ||
	    ev.window.event == SDL_WINDOWEVENT_FOCUS_LOST) {
	  paused = true;
	  SDL_LogWarn(SDL_LOG_CATEGORY_TEST, "paused");
	} else if (ev.window.event == SDL_WINDOWEVENT_RESTORED ||
	    ev.window.event == SDL_WINDOWEVENT_FOCUS_GAINED) {
	  paused = false;
	  SDL_LogWarn(SDL_LOG_CATEGORY_TEST, "restored");
	} else {
	  /*
	  sdllog(string_join{" "}("window event ",
		static_cast{int}(ev.window.event)));
	  */
	}
      } else if (etype == SDL_FINGERMOTION) {
	finger_motion(ev.tfinger.x, ev.tfinger.y, ev.tfinger.dx, ev.tfinger.dy,
	  ev.tfinger.fingerId);
	/*
	sdllog(string_join{" "}("fingermotion", ev.tfinger.x, ev.tfinger.y,
	  ev.tfinger.dx, ev.tfinger.dy, ev.tfinger.fingerId));
	*/
      } else if (etype == SDL_MOUSEBUTTONDOWN) {
	/*
	sdllog(string_join{" "}("mousedown", ev.button.x, ev.button.y,
	  ev.button.button));
	*/
	finger_down(
	  static_cast{float}(ev.button.x)
	    / static_cast{float}(cam.screen_width),
	  static_cast{float}(ev.button.y)
	    / static_cast{float}(cam.screen_height),
	  0);
      } else if (etype == SDL_MOUSEBUTTONUP) {
	finger_up(0);
      } else if (etype == SDL_FINGERDOWN) {
	finger_down(ev.tfinger.x, ev.tfinger.y, ev.tfinger.fingerId);
      } else if (etype == SDL_FINGERUP) {
	finger_up(ev.tfinger.fingerId);
      } else if (etype == SDL_APP_WILLENTERBACKGROUND) {
	SDL_LogWarn(SDL_LOG_CATEGORY_TEST, string_join{""}("paused"));
	paused = true;
      } else if (etype == SDL_APP_DIDENTERFOREGROUND) {
	SDL_LogWarn(SDL_LOG_CATEGORY_TEST, string_join{""}("resumed"));
	paused = false;
	int w;
	int h;
	SDL_GL_GetDrawableSize(wnd, w, h);
	reshape(cam, w, h);
      } else if (etype == SDL_QUIT) {
	done = true;
	break;
      } else {
	/*
	sdllog(string_join{" "}("unknown event", static_cast{int}(ev.type)));
	*/
      }
    }
    if (paused) {
      SDL_Delay(100);
      tprev = SDL_GetTicks();
      tprevsec = tprev;
      continue;
    }
    save_ticks(1);
    {
      const mess = string_join{" "}(fps, cam.camera_angle, ticks_sum_prev,
	frame_speed_mul);
      draw_frame(cam, sdr, bo, psdr, fti, mess);
      save_ticks(2);
      ++frames;
      uint t = SDL_GetTicks();
      if (t - tprevsec >= 1000) {
	const seconds = static_cast{float}(t - tprevsec) / 1000.0;
	fps = static_cast{float}(frames) / seconds;
	/*
	SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	  string_join{" "}(frames, "frames in", seconds, "seconds =",
	    fps, "fps"));
	SDL_LogWarn(SDL_LOG_CATEGORY_TEST,
	  string_join{" "}("ticks: ", ticks_sum));
	*/
	ticks_sum_prev = ticks_sum;
	ticks_sum = ticks_t();
	tprevsec = t;
	frames = 0;
      }
      mutable tdiff = static_cast{int}(t - tprev);
      tprev = t;

      uint tdiff_frame_sum = static_cast{uint}(tdiff);
      for (const i: 1 .. frame_tdiffs.size()) {
	tdiff_frame_sum += frame_tdiffs[i];
	frame_tdiffs[i - 1] = frame_tdiffs[i];
      }
      frame_tdiffs[frame_tdiffs.size() - 1] = static_cast{uint}(tdiff);

      if (tdiff_frame_sum < 98 || tdiff_frame_sum > 102) {
	frame_speed_mul = static_cast{float}(tdiff_frame_sum)
	  * 60.0f / 1000.0f / static_cast{float}(frame_tdiffs.size());
      }
      if (frame_speed_mul > 0.90f && frame_speed_mul < 1.10f) {
	frame_speed_mul = 1.0f;
      }
      /* println("frame_speed_mul", frame_speed_mul, tdiff_frame_sum,
       frame_tdiffs); */

      {
	SDL_KeyboardState const ks;
	if (ks.Key(SDL_SCANCODE_ESCAPE) != 0) {
	  done = true;
	}
	if (ks.Key(SDL_SCANCODE_UP) != 0) {
	  camera_rotate(1.0, 0.0, 0.0, 1.0);
	}
	if (ks.Key(SDL_SCANCODE_DOWN) != 0) {
	  camera_rotate(1.0, 0.0, 0.0, -1.0);
	}
	if (ks.Key(SDL_SCANCODE_LEFT) != 0) {
	  camera_rotate(0.0, 1.0, 0.0, 1.0);
	}
	if (ks.Key(SDL_SCANCODE_RIGHT) != 0) {
	  camera_rotate(0.0, 1.0, 0.0, -1.0);
	}
	if (ks.Key(SDL_SCANCODE_SPACE) != 0) {
	  camera_move(0.0, 0.0, -0.02);
	}
	if (ks.Key(SDL_SCANCODE_LSHIFT) != 0) {
	  camera_move(0.0, 0.0, 0.02);
	}
	if (ks.Key(SDL_SCANCODE_A) != 0) {
	  camera_move(-0.02, 0.0, 0.0);
	}
	if (ks.Key(SDL_SCANCODE_D) != 0) {
	  camera_move(0.02, 0.0, 0.0);
	}
	if (ks.Key(SDL_SCANCODE_S) != 0) {
	  camera_move(0.0, -0.02, 0.0);
	}
	if (ks.Key(SDL_SCANCODE_W) != 0) {
	  camera_move(0.0, 0.02, 0.0);
	}
	if (ks.Key(SDL_SCANCODE_Z) != 0) {
	  camera_rotate(0.0, 0.0, 1.0, -1.0);
	}
	if (ks.Key(SDL_SCANCODE_X) != 0) {
	  camera_rotate(0.0, 0.0, 1.0, 1.0);
	}
	finger_action_step();
      }
    }
    save_ticks(3);
    SDL_GL_SwapWindow(wnd);
    save_ticks(4);
  }
  io::system._exit(0);
}

