public namespace svr;
import common -;
import io -;
import io::file -;
import io::errno -;
import io::standard -;
import io::addrinfo -;
import io::socket -;
import io::signal -;
import thread -;

private metafunction svr_host "127.0.0.1";
private metafunction svr_port "5001";

multithreaded struct server_thread(io const& i, tptr{bool} const& rp,
  tptr{string} const& s)
{
  io const iop = i;
  tptr{bool} readyp = rp;
  tptr{string} str_r = s;
  function void notify()
  {
    mutable lck = lock_guard{bool}(readyp);
    *lck = true;
    lck.notify_one();
  }
  function void main()
  {
    try {
      iop.stderr().println("svr");
      const sf = iop.make_acceptor(svr_host, svr_port, AF_INET,
	SOCK_STREAM, 1024, 0); // TODO: fix EADDRINUSE
      iop.stderr().println("accepted");
      notify();
      mutable peer_addr = sockaddr_storage();
      const sock = sf.value.accept(peer_addr);
      string s;
      size_t len = 10;
      sock.value.read(s, len);
      *str_r = s;
    } catch (exception::c_exception e) {
      iop.stderr().println("thrown");
      notify();
    } catch (io::errno::errno_t e) {
      iop.stderr().println("thrown errno");
      notify();
    }
  }
}

multithreaded struct client_thread(io const& i, tptr{bool} const& rp)
{
  io const iop = i;
  tptr{bool} readyp = rp;
  function void main()
  {
    {
      mutable lck = lock_cguard{bool}(readyp);
      while (true) {
	if (*lck) { break; }
	lck.wait();
      }
    }
    iop.stderr().println("got notification");
    const sf = iop.make_connector(svr_host, svr_port, AF_INET,
      SOCK_STREAM, 10);
    const sock = sf.value.connect();
    sock.value.write("hoge");
  }
}

function void t1()
{
  io::system.signal(SIGPIPE, SIG_IGN);
  const readyp = make_tptr(bool(false));
  const strp = make_tptr(string());
  const sthr = make_thread(server_thread(io::system, readyp, strp));
  const cthr = make_thread(client_thread(io::system, readyp));
  sthr.join();
  cthr.join();
  println(*strp);
}

t1();

