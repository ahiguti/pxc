public namespace sock4;
import common -;
import meta -;
import io -;
import io::file -;
import io::errno -;
import io::standard -;
import io::addrinfo -;
import io::socket -;
import io::signal -;
import thread -;
import orb -;
import transport_file -;

public threaded interface ifoo <remote> {
  public function int fold(cslice{int} const& x, int v0, ptr{ibinop} const& o);
}

public threaded struct foo <ifoo> {
  public function int fold(cslice{int} const& x, int v0, ptr{ibinop} const& o)
  {
    mutable r = v0;
    for (size_t i, int v : x) {
      r = o->op(r, v);
    }
    return r;
  }
}

public threaded interface ibinop <remote> {
  public function int op(int x, int y);
}

public threaded struct binop_add <ibinop> {
  public function int op(int x, int y) {
    return x + y;
  }
}

private function void client_test_main(file_mt const& f)
{
  const op = make_ptr{orb_handle{ifoo}}(make_ptr{transport_file}(f));
  const st = op->create_stub();
  farray{int, 10} arr;
  for (int i : 0 .. 10) { arr[static_cast{size_t}(i)] = i + 1; }
  const o = make_ptr(binop_add());
  const z = st->fold(arr, 0, o); /* o is called back from the server */
  println(z);
}

private multithreaded struct server_thread(io const& s, file_mt const& f) {
  io const sys = s;
  file_mt const sock = f;
  public function void main() {
    const op = make_ptr{orb_handle{ifoo}}(make_ptr{transport_file}(sock));
    const obj = make_ptr(foo());
    op->start_server(obj);
  }
}

private function void main()
{
  io::system.signal(SIGPIPE, SIG_IGN);
  mutable sp = io::system.socketpair(AF_UNIX, SOCK_STREAM, PROTO_UNSPEC);
  const sthr = make_thread(server_thread(io::system, sp.value.first));
  client_test_main(sp.value.second);
  sp.errno = errno_t(); /* closes fds */
}

main();

