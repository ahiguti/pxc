public namespace bsearch;
import common -;
import meta m;

private metafunction cidx{lst} m::div{m::size{lst}, 2};
private metafunction toph{lst} m::slice{lst, 0, cidx{lst}};
private metafunction both{lst} m::slice{lst, m::add{cidx{lst}, 1}};

function {lst} void bsinl(int x)
{
  if (m::gt{m::size{lst}, 0}) {
    if (x == m::at{lst, cidx{lst}}) {
      string s = "found: ";
      s.append(to_string(x));
      println(s);
    } else if (x < m::at{lst, cidx{lst}}) {
      bsinl{toph{lst}}(x);
    } else {
      bsinl{both{lst}}(x);
    }
  } else {
    string s = "notfound: ";
    s.append(to_string(x));
    println(s);
  }
}

function void test1(int x)
{
  metafunction lst m::sort{m::list{1, 5, 23, 11, 4, 9, 16}};
  bsinl{lst}(x);
}

function void t1()
{
  for (int i : 0 .. 30) {
    test1(i);
  }
}

t1();

