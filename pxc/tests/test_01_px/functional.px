
namespace functional;
import common -;
import meta m;
import numeric::cast -;

function {tdst, f} void array_construct(tdst mutable& dst, size_t sz)
{
  dst.resize(sz, m::rettype{f}());
  size_t i;
  for (i = 0; i < sz; ++i) {
    dst[i] = f(numeric_cast{m::argtype{f, 0}, size_t}(i));
  }
}

function {tsrc, tdst, f} void array_apply(tsrc src, tdst mutable& dst)
{
  size_t sz = src.size();
  dst.resize(sz, m::rettype{f}());
  size_t i;
  for (i = 0; i < sz; ++i) {
    dst[i] = f(src[i]);
  }
}

function {tsrc, tv, f} tv array_foldl(tsrc src, tv v)
{
  size_t sz = src.size();
  tv r = v;
  size_t i;
  for (i = 0; i < sz; ++i) {
    r = f(r, src[i]);
  }
  return r;
}

function {tsrc, tv, f} tv array_foldr(tsrc src, tv v)
{
  size_t sz = src.size();
  tv r = v;
  size_t i;
  for (i = sz; i > 0; --i) {
    r = f(src[i - 1], r);
  }
  return r;
}

function {t} t id(t v) { return v; }
function {t} t square(t v) { return v * v; }
function {t} t add(t x, t y) { return x + y; }

function {t} void test1()
{
  metafunction vtype t;
  metafunction arrtype vector{vtype};
  arrtype x;
  array_construct{arrtype, id{vtype}}(x, 11);
  arrtype y;
  array_apply{arrtype, arrtype, square{vtype}}(x, y);
  vtype r1 = array_foldl{arrtype, vtype, add{vtype}}(y, 0);
  vtype r2 = array_foldr{arrtype, vtype, add{vtype}}(y, 0);
  println(r1);
  println(r2);
}
test1{int}();

