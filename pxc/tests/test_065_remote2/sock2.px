public namespace sock2;
import common -;
import meta -;
import io -;
import io::file -;
import io::errno -;
import io::standard -;
import io::addrinfo -;
import io::socket -;
import io::signal -;
import thread -;
import orb -;

public threaded interface ifoo <remote> {
  public function int hoge(int x, int y) const;
  public function string fuga(cstrref const& x, cstrref const& y) const;
  public function ptr{ibar} create_bar(int x) const;
}

public threaded interface ibaz <remote> {
  public function int baz_m1(int x);
}

public threaded struct baz <ibaz> {
  public function int baz_m1(int x) {
    // debug_log("baz_m1", x);
    return x * x;
  }
}

public threaded interface ibar <remote> {
  public function int m1(int y);
  public function void set_baz(ptr{ibaz} const& p);
  public function int do_baz(int v);
}

public threaded struct bar(int x0) <ibar> {
  public int x = x0;
  public ptr{ibaz} bzp = make_ptr(baz());
  public function int m1(int y) {
    return x * y;
  }
  public function void set_baz(ptr{ibaz} const& p) {
    bzp = p;
  }
  public function int do_baz(int v) {
    return bzp->baz_m1(v);
  }
}

public threaded struct foo <ifoo> {
  public int intval;
  public string strval;
  public function int hoge(int x, int y) const {
    return intval + x + y;
  }
  public function string fuga(cstrref const& x, cstrref const& y) const {
    string s = strval;
    s.append(x);
    s.append(y);
    return s;
  }
  public function ptr{ibar} create_bar(int x) const {
    ptr{bar} p = make_ptr{bar}(bar(x));
    return p;
  }
}

private threaded struct transport_file(file_mt const& f0) <orb_transport> {
  file_mt const f = f0;
  bool ok = true;
  public function void send_receive(string mutable& rbuf,
    string mutable& wbuf) {
    if (ok && !wbuf.empty()) {
      const wr = f.write_all(wbuf);
      if (case wr.errno || wr.value != wbuf.size()) {
	ok = false;
      }
    }
    wbuf.clear();
    rbuf.clear();
    while (ok) {
      size_t rsize = 1024;
      const rr = f.read(rbuf, rsize);
      if (rbuf.size() > 0 && rbuf[rbuf.size() - 1] == '\n') {
	break;
      }
      if (case rr.errno || rr.value == 0) {
	ok = false;
      }
    }
    if (!ok) {
      rbuf.clear();
      rbuf = "T\tio_error\n";
    }
  }
}

private threaded function ptr{orb} create_orb_file(file_mt const& f)
{
  return make_ptr{orb}(make_ptr{transport_file}(f));
}

threaded struct orb_cleanup(ptr{orb} const& op0) {
  ptr{orb} op = op0;
  function ~ {
    // debug_log("clear", 1);
    op->clear_skeletons();
  }
}

private threaded function {tinterf, tklass} void
orb_server_loop(file_mt const& f)
{
  ptr{orb_transport} tf = make_ptr(transport_file(f));
  ptr{orb} op = make_ptr(orb(tf));
  orb_cleanup opclean = orb_cleanup(op);
  ptr{tinterf} obj = make_ptr(tklass());
  push_return_value(op, obj);
  exchange_loop(op);
}

private threaded function {tinterf} ptr{tinterf} create_stub(
  ptr{orb} const& op)
{
  exchange_loop(op);
  return pop_return_value{ptr{tinterf}}(op);
}

private function void client_test_main(file_mt const& f)
{
  const op = make_ptr{orb}(make_ptr{transport_file}(f));
  const opclean = orb_cleanup(op);
  const st = create_stub{ifoo}(op);
  const z = st->hoge(5, 33);
  const s = st->fuga("abc", "xyz");
  const bp = st->create_bar(55);
  const w = bp->m1(100);
  const bzp = make_ptr{baz}(baz());
  bp->set_baz(bzp);
  const v = bp->do_baz(99);
  println(z);
  println(s);
  println(w);
  println(v);
}

private multithreaded struct server_thread(io const& s, file_mt const& f) {
  io const sys = s;
  file_mt const sock = f;
  public function void main() {
    orb_server_loop{ifoo, foo}(sock);
  }
}

private function void main()
{
  io::system.signal(SIGPIPE, SIG_IGN);
  mutable sp = io::system.socketpair(AF_UNIX, SOCK_STREAM, PROTO_UNSPEC);
  const sthr = make_thread(server_thread(io::system, sp.value.first));
  client_test_main(sp.value.second);
  sp.errno = errno_t(); /* closes fds */
}

main();

