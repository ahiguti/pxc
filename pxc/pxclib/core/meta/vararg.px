public threaded namespace core::meta::vararg;
public import core::meta m;

/*
  Usage:
    function {t0, tlist} t0
    foo(expand(meta::vararg::arg_decls_bycref{tlist}))
    {
      mutable r = t0();
      expand(a: meta::vararg::arg_names{meta::size{tlist}}) {
        r += a;
      }
      return r;
    }
    {
      println(foo{int}(2, 3, 4)); // prints 9
    }
 */

public metafunction arg_decls_bycref{tilist}
  m::map{
    m::seq{m::size{tilist}},
    metafunction{i}
      m::list{
        m::concat{"a", i}, m::at0{m::at{tilist, i}}, 1,
        0}}; /* always const */

public metafunction arg_decls_byref{tilist}
  m::map{
    m::seq{m::size{tilist}},
    metafunction{i}
      m::list{
        m::concat{"a", i}, m::at0{m::at{tilist, i}}, 1,
        m::at1{m::at{tilist, i}}}}; /* preserve constness */

public metafunction arg_names{sz}
  m::map{m::seq{sz}, metafunction{x} m::concat{"a", x}};

public metafunction field_names{sz}
  m::map{m::seq{sz}, metafunction{x} m::concat{"_", x}};

