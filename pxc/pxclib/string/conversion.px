namespace string::conversion;
public import numeric::integral -;
public import container::string -;
public import meta::common m;
public import meta::family mf;
public import numeric::cast n;
public import string::positional p;
public import string::serialize -;
public import string::url_encode u;
// public import string::json_encode j;

public threaded function {t} string to_string(t const& x)
{
  if (is_string_family_type{t}) {
    return x;
  } else {
    string s;
    serialize_to_string(x, s);
    return s;
  }
}

public threaded function {t} void serialize_to_string(t const& x, string& out)
{
  serialize{deftmpl, t}(x, out);
}

public metafunction is_string_family_type{t}
    m::or{
      m::eq{t, string},
      m::eq{t, strlit},
      m::eq{t, strref},
      m::eq{t, cstrref}};

public threaded function void bool_to_string(bool v, string& buf)
{
  if (v) {
    buf.append("true");
  } else {
    buf.append("false");
  }
}

private threaded function {t} void string_append_escape(t const& x, string& v)
{
  // j::json_encode(x, v);
  v.push_back('"');
  u::url_encode(v, x);
  v.push_back('"');
}
private threaded function {t} void string_append_noesc(t const& x, string& v)
{
  v.append(x);
}
private threaded function void string_append_brace_open(string& v)
{ v.push_back('{'); }
private threaded function void string_append_brace_close(string& v)
{ v.push_back('}'); }
private threaded function void string_append_comma(string& v)
{ v.push_back(','); }
private threaded function void string_append_equal(string& v)
{ v.push_back('='); }
private threaded function void string_append_none(string& v)
{ } 
private threaded function {t} void string_append_star(t const& x, string& v)
{ v.push_back('*'); }
private threaded function {t} void string_append_unknown(t const& x, string& v)
{ v.push_back('?'); }

private threaded struct deftmpl {
  metafunction target_type string;
  metafunction symstr{t} m::concat{t, "_append_to_string"};
  metafunction sym{t} m::symbol{symstr{t}, t};
  metafunction has_specialized_symbol{t} m::eq{m::to_string{sym{t}}, symstr{t}};
  metafunction specialized_func{t}
    m::cond{has_specialized_symbol{t}, sym{t},
    m::cond{is_string_family_type{t}, string_append_escape{t},
    m::cond{n::is_integral{t}, p::integral_to_decimal,
    m::cond{n::is_fp{t}, p::fp_to_decimal,
    m::cond{m::eq{t, bool}, bool_to_string,
    m::cond{m::eq{t, unit}, string_append_star,
    0}}}}}};
  metafunction array_begin string_append_brace_open;
  metafunction array_end string_append_brace_close;
  metafunction array_sep string_append_comma;
  metafunction map_begin string_append_brace_open;
  metafunction map_end string_append_brace_close;
  metafunction map_sep string_append_comma;
  metafunction map_entry_begin string_append_none;
  metafunction map_entry_end string_append_none;
  metafunction map_entry_sep string_append_equal;
  metafunction pointer_begin string_append_brace_open;
  metafunction pointer_end string_append_brace_close;
  metafunction struct_begin string_append_brace_open;
  metafunction struct_end string_append_brace_close;
  metafunction struct_sep string_append_comma;
  metafunction struct_entry_begin string_append_none;
  metafunction struct_entry_end string_append_none;
  metafunction struct_entry_sep string_append_equal;
  metafunction struct_field_name string_append_noesc;
  metafunction union_begin string_append_brace_open;
  metafunction union_end string_append_brace_close;
  metafunction union_entry_sep string_append_equal;
  metafunction union_field_name string_append_noesc;
  metafunction unknown{fam} string_append_unknown;
}

