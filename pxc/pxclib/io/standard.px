namespace io::standard;
public import numeric::integral -;
public import container::string -;
public import io -;
public import io::file -;
public import io::errno -;
public import string::conversion;
public import meta::common m;

public extern "pxcio::pxc_stdin"  file stdin;
public extern "pxcio::pxc_stdout" file stdout;
public extern "pxcio::pxc_stderr" file stderr;
public threaded function extern "pxcio::get_stdin"
  file get_stdin(io const& iop);
public threaded function extern "pxcio::get_stdout"
  file get_stdout(io const& iop);
public threaded function extern "pxcio::get_stderr"
  file get_stderr(io const& iop);

private function extern "pxcio::iostd_init" void iostd_init();

iostd_init();

public threaded function {t} void file_print(file const& f, t const& x)
{
  errno_t e;
  if (is_string_family_type{t}) {
    file_write_all(f, x, e);
  } else {
    file_write_all(f, string::conversion::to_string(x), e);
  }
}

public threaded function {t} void file_println(file const& f, t const& x)
{
  errno_t e;
  if (is_string_family_type{t}) {
    string s = x;
    s.append("\n");
    file_write_all(f, s, e);
  } else {
    string s = string::conversion::to_string(x);
    s.append("\n");
    file_write_all(f, s, e);
  }
}

public function {t} void print(t const& x)
  { file_print(stdout, x); }
public function {t} void println(t const& x)
  { file_println(stdout, x); }

extern "type" inline
namespace pxcio {
};

extern "fdecl" inline
namespace pxcio {
extern file pxc_stdin;
extern file pxc_stdout;
extern file pxc_stderr;
};

extern "fdef" inline
namespace pxcio {
file pxc_stdin;
file pxc_stdout;
file pxc_stderr;
file get_stdin(io const& iop)  { return pxc_stdin;  }
file get_stdout(io const& iop) { return pxc_stdout; }
file get_stderr(io const& iop) { return pxc_stderr; }
void iostd_init()
{
  static int init_flag = 0;
  if (!init_flag) {
    init_flag = 1;
    pxc_stdin  = create_file(0);
    pxc_stdout = create_file(1);
    pxc_stderr = create_file(2);
  }
}
};

