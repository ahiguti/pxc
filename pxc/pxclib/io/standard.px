namespace io::standard;
public import type::builtin "";
public import io "";
public import serialize::string;
public import meta::builtin m;
public import io::file "";

public extern "pxcio::stdin" fileptr stdin;
public extern "pxcio::stdout" fileptr stdout;
public extern "pxcio::stderr" fileptr stderr;
public threaded extern "pxcio::get_stdin"
  function fileptr get_stdin(ioptr const& iop);
public threaded extern "pxcio::get_stdout"
  function fileptr get_stdout(ioptr const& iop);
public threaded extern "pxcio::get_stderr"
  function fileptr get_stderr(ioptr const& iop);

private extern "pxcio::iostd_init" function void iostd_init();

iostd_init();

public threaded function {t}
void file_print(file& f, t const& x)
{
  if (m::eq{t, type::builtin::string}) {
    f.write_all(x);
  } else {
    f.write_all(serialize::string::to_string(x));
  }
}

public threaded function {t}
void file_println(file& f, t const& x)
{
  if (m::eq{t, type::builtin::string}) {
    string s = x;
    s.append("\n");
    f.write_all(s);
  } else {
    string s = serialize::string::to_string(x);
    s.append("\n");
    f.write_all(s);
  }
}

public function {t}
void print(t const& x)
{
  file_print(*stdout, x);
}

public function {t}
void println(t const& x)
{
  file_println(*stdout, x);
}

extern "type" inline
namespace pxcio {
};

extern "fdecl" inline
namespace pxcio {
typedef pxcrt::rcptr< pxcrt::trcval<file_impl> > fileptr;
extern fileptr stdin;
extern fileptr stdout;
extern fileptr stderr;
};

extern "fdef" inline
namespace pxcio {
fileptr stdin;
fileptr stdout;
fileptr stderr;
fileptr get_stdin(ioptr const& iop) { return stdin; }
fileptr get_stdout(ioptr const& iop) { return stdout; }
fileptr get_stderr(ioptr const& iop) { return stderr; }
void iostd_init()
{
  static int init_flag = 0;
  if (!init_flag) {
    init_flag = 1;
    stdin = fileptr(pxcrt::boxing());
    stdout = fileptr(pxcrt::boxing());
    stderr = fileptr(pxcrt::boxing());
    stdin->value$z.reset(0);
    stdout->value$z.reset(1);
    stderr->value$z.reset(2);
  }
}
};

