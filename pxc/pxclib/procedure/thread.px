public threaded namespace procedure::thread "use-unsafe";
public import numeric::integral -;
public import operator -;
public import pointer -;
public import io::errno -;
public import generic::datatype -;
public import meta m;
public import exception -;
public import procedure::callable -;
public import text::serialize -;

public threaded function {f} callable_ptr{m::ret_type{f}, m::nil}
make_thread_ptr(expand(m::args{f}))
{
  return callable_ptr{m::ret_type{f}, m::nil}(
    make_ptr{thread{f}}(expand(a: m::arg_names{f}; a)));
    /* arg types of f must be multithreaded, or an error is raised here. */
}

public threaded struct {f}
thread(expand(m::args{f}))
  <callable{m::ret_type{f}, m::nil}>
{
  private metafunction tfuncobj thread_main_funcobj{f};
  private tfuncobj mutable fobj = tfuncobj(expand(a: m::arg_names{f}; a));
  private bool need_join = false;
  private thread_ptr thd;
  init();
  private function void init() {
    const err = thread_create{tfuncobj}(thd, fobj);
    if (err != EOK) {
      throw runtime_error_template{"thread_create"}(to_string(err));
    }
    need_join = true;
  }
  public function ~ {
    call__();
  }
  public function m::ret_type{f} call__() {
    if (need_join) {
      need_join = false;
      const err = thread_join{tfuncobj}(thd);
      if (err != EOK) {
	throw runtime_error_template{"thread_join"}(to_string(err));
      }
    }
    if (m::ne{m::ret_type{f}, void}) {
      return fobj.ret.some;
    }
  }
}

private multithreaded struct {f}
thread_main_funcobj(expand(m::args{f}))
{
  private metafunction tfld tuple{m::arg_types{f}, m::nil};
  private metafunction tret
    m::cond{m::eq{m::ret_type{f}, void}, unit, option{m::ret_type{f}}};
  public tfld const fld = tfld(expand(a: m::arg_names{f}; a));
    /* arg types of f must be multithreaded, or an error is raised here. */
  public tret mutable ret;
  public function void call__() {
    if (m::eq{m::ret_type{f}, void}) {
      f(expand(f: m::field_names{tfld}; fld.f));
    } else {
      ret.some = f(expand(f: m::field_names{tfld}; fld.f));
    }
  }
}

private threaded struct extern "pxcrt::thread_ptr" "nonmovable" thread_ptr { }

private threaded function extern "pxcrt::thread_create" {tfuncobj} errno_t
thread_create(thread_ptr mutable& thd, tfuncobj mutable& funcobj);
private threaded function extern "pxcrt::thread_join" {tfuncobj} errno_t
thread_join(thread_ptr mutable& thd);

extern "types" inline

#include <thread>
#include <system_error>

namespace pxcrt {

typedef std::thread *thread_ptr;

};

;

extern "functions" inline

namespace pxcrt {

template <typename Tfuncobj> static inline void *
funcobj_wrap(void *arg)
{
  Tfuncobj *const a = static_cast<Tfuncobj *>(arg);
  a->call__$f();
  return 0;
}

template <typename Tfuncobj> int
thread_create(thread_ptr& thd, Tfuncobj& funcobj)
{
#ifndef PXC_NO_THREAD
  thd = new std::thread(&funcobj_wrap<Tfuncobj>, &funcobj);
#endif
  return 0;
}

template <typename Tfuncobj> int
thread_join(thread_ptr& thd)
{
  if (thd) {
    thd->join();
  }
  return 0;
}

};

;

